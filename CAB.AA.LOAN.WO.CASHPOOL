    SUBROUTINE CAB.AA.LOAN.WO.CASHPOOL(RET.ARRAY)

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_ENQUIRY.COMMON
    $INSERT I_F.AA.ACCOUNT
    $INSERT I_F.AA.ARRANGEMENT

    GOSUB INIT
    GOSUB GET.DATA
    GOSUB PROCESS

    RETURN


INIT:

    EFF.DATE = TODAY
    FN.AA.ARR= 'F.AA.ARRANGEMENT'
    F.AA.ARR = ''
    CALL OPF(FN.AA.ARR,F.AA.ARR)

    FN.AC.CASHPOOL = 'F.AC.CASH.POOL'
    F.AC.CASHPOOL = ''
    CALL OPF(FN.AC.CASHPOOL,F.AC.CASHPOOL)


    POS = '';
    CALL MULTI.GET.LOC.REF('AA.ARR.ACCOUNT','LEGAL.STATUS',POS)

    SEL.TEXT = ' WITH PRODUCT.LINE EQ LENDING AND ARR.STATUS NE PENDING.CLOSURE AND ARR.STATUS NE CLOSE'

    LOCATE 'CUSTOMER.NO' IN ENQ.SELECTION<2,1> SETTING CUS.POS THEN
        CUS.ID = ENQ.SELECTION<4,CUS.POS>
        SEL.TEXT<-1>=' AND CUSTOMER EQ ':CUS.ID
    END

    LOCATE 'ARRANGEMENT.ID' IN ENQ.SELECTION<2,1> SETTING ARR.POS THEN
        ARR.ID = ENQ.SELECTION<4,ARR.POS>
        SEL.TEXT<-1>=' AND @ID EQ ' : ARR.ID
    END

    LOCATE 'BRANCH.NAME' IN ENQ.SELECTION<2,1> SETTING BR.POS THEN
        BR.CODE = ENQ.SELECTION<4,BR.POS>
        SEL.TEXT<-1> = ' AND CO.CODE EQ ': BR.CODE
    END

    LOCATE 'PRODUCT.ID' IN ENQ.SELECTION<2,1> SETTING PRD.POS THEN
        PRO.NAME = ENQ.SELECTION<4,PRD.POS>
        SEL.TEXT<-1> = ' AND PRODUCT.NAME EQ ' : PRO.NAME
    END
    CONVERT FM TO ' ' IN SEL.TEXT
    RETURN

GET.DATA:

    SEL.CMD = 'SELECT ' :FN.AA.ARR : SEL.TEXT
    SEL.LIST = '';
    CALL EB.READLIST(SEL.CMD,SEL.LIST,'',NO.OF.REC,SEL.ERR)


    RETURN
PROCESS:

    LOOP
        REMOVE ARR.ID FROM SEL.LIST SETTING SEL.POS
    WHILE ARR.ID : SEL.POS
        ARR.REC = '';PRODUCT.ID = '';PROP.LIST = '';
        CALL AA.GET.ARRANGEMENT.PRODUCT (ARR.ID,EFF.DATE,ARR.REC,PRODUCT.ID,PROP.LIST)
        CO.CODE = ARR.REC<AA.ARR.CO.CODE>
        CUS.ID  = ARR.REC<AA.ARR.CUSTOMER>
        L.CURR  = ARR.REC<AA.ARR.CURRENCY>
        L.C.DATE= ARR.REC<AA.ARR.START.DATE>
        ACC.NO  = ARR.REC<AA.ARR.LINKED.APPL.ID>

        CALL F.READ(FN.AC.CASHPOOL,ACC.NO,R.AC.CASHREC,F.AC.CASHPOOL,ERR.CASH)
        IF ERR.CASH THEN
            r.Ids = ''; custCondition = ''; L.STATUS = ''; r.Error = '';
            CALL AA.GET.ARRANGEMENT.CONDITIONS(ARR.ID, 'ACCOUNT', '', '', r.Ids, custCondition, r.Error)
            custCondition = RAISE(custCondition)
            L.STATUS = custCondition<AA.AC.LOCAL.REF><1,POS>
            RET.ARRAY<-1> = CO.CODE :"*":PRODUCT.ID:"*":ARR.ID:"*":ACC.NO:"*":L.CURR:"*":CUS.ID:"*":L.C.DATE:"*":L.STATUS

        END
    REPEAT

    RETURN
END
