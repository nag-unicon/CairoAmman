    SUBROUTINE CAB.B.CLOSED.ACCTS
*-----------------------------------------------------------------------------
*Modification Details:
*=====================
* Modified by        Date              Description
* Nuha Tuhul        30-1-2019      Send the Messge to E SIG MQ
*-----------------------------------------------------------------------------
    $INCLUDE T24.BP I_COMMON
    $INCLUDE T24.BP I_EQUATE
    $INCLUDE T24.BP I_F.DATES

    GOSUB INIT
    GOSUB PROCESS
    RETURN

*****
INIT:
*****
    F.ACCOUNT.CLOSED = ''
    FN.ACCOUNT.CLOSED = 'F.ACCOUNT.CLOSED'
    CALL OPF(FN.ACCOUNT.CLOSED,F.ACCOUNT.CLOSED)

    FILE.PATH = 'Extract'
    FILE.NAME = 'Closed.txt.':R.DATES(EB.DAT.LAST.WORKING.DAY)
    OPENSEQ FILE.PATH,FILE.NAME TO FILE.CLOS ELSE
        CREATE FILE.CLOS ELSE
            STOP
        END
    END
    RETURN


********
PROCESS:
********
    CMD.ERR = '';NO.OF.R=''
    Y.TIME = TIMEDATE()
    Y.TIME =Y.TIME[1,8]

    SEL.CMD = 'SELECT ' : FN.ACCOUNT.CLOSED : ' WITH ACCT.CLOSE.DATE EQ ' : R.DATES(EB.DAT.LAST.WORKING.DAY)
    CALL EB.READLIST ( SEL.CMD,SEL.LIST ,'',NO.OF.REC,RET.CODE)

    LOOP
        REMOVE ACCT.NO FROM SEL.LIST SETTING POS
    WHILE ACCT.NO:POS
        WRITESEQ ACCT.NO APPEND TO FILE.CLOS ELSE
            CRT "ERROR IN WRITE"
        END

        YR.XML.OUT = ''
        YR.XML.OUT = "<?xml version='1.0' encoding='utf-8'?><Root><PsSystem>PS-SIG</PsSystem>"
        YR.XML.OUT:= "<RequestDate>":TODAY:"</RequestDate>"
        YR.XML.OUT:= "<RequestTime>":Y.TIME:"</RequestTime><RequestCode>AccountStatus</RequestCode>"
        YR.XML.OUT:= "<AccountNumber>":ACCT.NO:"</AccountNumber><StatusofAccount>3</StatusofAccount>"
        YR.XML.OUT:= "</Root>"

        MSG.ERR = ''
        CALL CAB.SEND.EXT.MSG(YR.XML.OUT,"SIG",MSG.ERR)

    REPEAT
    RETURN
END
