* Version 1.2
* Start on 23/04/2012 By A.Gryaznov, Temenos

    SUBROUTINE B.CAB.ATM.CONCAT.MSG

************************************************************************
* Program for concatenating daily Card messages. It is used if
* CAB.ATM.PARAMETER>DEVICE.TYPE is FILE.
* This COB routine will
* - Select the files in folder ATM.MNGM/TMP.
* - Concatenate all the flat files and produce a single file in the folder
*   defined in the CAB.ATM.PARAMETER record DEVICE.DETS.FILE.
* Developed for ATM Interface.
*
*                  ---- MODIFICATIONS ---------
* 20.06.2012 by agryaznov@temenos.com
* - Use LOC.TODAY instead of TODAY
************************************************************************

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_BATCH.FILES
    $INSERT I_F.BATCH
    $INSERT I_F.CAB.ATM.PARAMETER


    GOSUB INITIALIZE
    IF LOC.DEVICE.TYPE NE 'FILE' THEN RETURN
    IF NOT(LOC.OUT.DIR) THEN RETURN

    GOSUB PROCESS

    RETURN


************************************************************************

PROCESS:

*Concatenate
*LOC.CAT.CMD = 'SH "cat ./':LOC.TMP.DIR:'/* > ./':LOC.OUT.DIR:'/':LOC.TODAY:'"'
*EXECUTE LOC.CAT.CMD
*CRT LOC.CAT.CMD

*Clear TMP
*LOC.DEL.CMD = "CLEAR.FILE ":LOC.TMP.DIR
*EXECUTE LOC.DEL.CMD
*CRT LOC.DEL.CMD


    OPEN LOC.TMP.DIR TO LOC.F.TMP.FILE ELSE RETURN
    OPEN LOC.OUT.DIR TO LOC.F.OUT.FILE ELSE RETURN

    IF BATCH.STAGE THEN
        CALL CDT('', LOC.TODAY, '-1W')
    END

    LOC.FNAME    = 'MSG.':LOC.TODAY
    LOC.ALLRECS  = ''

* Customer file

    LOC.SEL.ACC = 'SELECT ':LOC.TMP.DIR:' LIKE MSG.':LOC.TODAY:'.AcCuBatch...'
*    LOC.SEL.ACC = 'SELECT ':LOC.TMP.DIR:' LIKE MSG.':LOC.TODAY:'.UserBatch...'
    CRT LOC.SEL.ACC

    EXECUTE LOC.SEL.ACC
    LOC.SYS.RET.CODE = @SYSTEM.RETURN.CODE
    LOC.SELECTED = @SELECTED
    READLIST LOC.KEY.LIST ELSE LOC.KEY.LIST = ''
    LOC.KEY.LIST = SORT(LOC.KEY.LIST)

    LOC.FNAME = 'MSG.':LOC.TODAY:'.AcCuBatch'
    LOC.RECS  = ''
    GOSUB MAKE.CONCAT

    IF LOC.RECS THEN
        LOC.RECS     = LOC.HEADER:FM:'<CustomerBatch>':FM:LOC.RECS:FM:'</CustomerBatch>'
*LOC.ALLRECS := LOC.RECS
        WRITE LOC.RECS TO LOC.F.OUT.FILE,LOC.FNAME
    END

* Card file

    LOC.SEL.CRD = 'SELECT ':LOC.TMP.DIR:' LIKE MSG.':LOC.TODAY:'.CardBatch...'
    CRT LOC.SEL.CRD

    EXECUTE LOC.SEL.CRD
    LOC.SYS.RET.CODE = @SYSTEM.RETURN.CODE
    LOC.SELECTED = @SELECTED
    READLIST LOC.KEY.LIST ELSE LOC.KEY.LIST = ''
    LOC.KEY.LIST = SORT(LOC.KEY.LIST)

    LOC.FNAME = 'MSG.':LOC.TODAY:'.CardBatch'
    LOC.RECS  = ''
    GOSUB MAKE.CONCAT

    IF LOC.RECS THEN
        LOC.RECS     = LOC.HEADER:FM:'<CardBatch>':FM:LOC.RECS:FM:'</CardBatch>'
*LOC.ALLRECS := LOC.RECS
        WRITE LOC.RECS TO LOC.F.OUT.FILE,LOC.FNAME
    END

***

    CLOSE LOC.TMP.DIR
    CLOSE LOC.OUT.DIR

    RETURN

*************************************************************************

MAKE.CONCAT:

    LOOP
        REMOVE LOC.FILE.NAME FROM LOC.KEY.LIST SETTING LOC.MORE
    WHILE LOC.FILE.NAME:LOC.MORE
        READ LOC.R.REC FROM LOC.F.TMP.FILE,LOC.FILE.NAME ELSE RETURN
        LOC.RECS<-1> = LOC.R.REC
        DELETE LOC.F.TMP.FILE,LOC.FILE.NAME
    REPEAT

    RETURN

*************************************************************************

INITIALIZE:

    LOC.TMP.DIR = "ATM.MNGM/TMP"

    LOC.HEADER = '<?xml version="1.0" encoding="us-ascii"?>'

    OPEN "F.CAB.ATM.PARAMETER" TO F.CAB.ATM.PARAMETER ELSE
        CRT "Can not open a file F.CAB.ATM.PARAMETER"
        RETURN
    END

    LOC.DEVICE.TYPE = ''
    READ R.CAB.ATM.PARAMETER FROM F.CAB.ATM.PARAMETER, 'DEVICE.TYPE' ELSE
        CRT "Can not read a record DEVICE.TYPE from CAB.ATM.PARAMETER"
        CLOSE F.CAB.ATM.PARAMETER
        RETURN
    END
    LOC.DEVICE.TYPE = R.CAB.ATM.PARAMETER<CAP.VALUE,1>

    LOC.OUT.DIR = ''
    READ R.CAB.ATM.PARAMETER FROM F.CAB.ATM.PARAMETER, 'DEVICE.DETS.FILE' ELSE
        CRT "Can not read a record DEVICE.DETS.FILE from CAB.ATM.PARAMETER"
        CLOSE F.CAB.ATM.PARAMETER
        RETURN
    END
    LOC.OUT.DIR = R.CAB.ATM.PARAMETER<CAP.VALUE,1>:R.CAB.ATM.PARAMETER<CAP.VALUE,2>

    CLOSE F.CAB.ATM.PARAMETER

    BATCH.ID = BATCH.INFO<1>
    JOB.NAME = BATCH.INFO<3>
    BATCH.STAGE = ''

    F.BATCH = ''
    CALL OPF("F.BATCH", F.BATCH)
    BATCH.REC = ''
    READ.ERR = ''
    CALL F.READ("F.BATCH", BATCH.ID, BATCH.REC, F.BATCH, READ.ERR)
    IF READ.ERR THEN CRT READ.ERR ELSE
        BATCH.STAGE = BATCH.REC<BAT.BATCH.STAGE>
    END

    LOC.TODAY = TODAY

    RETURN

*************************************************************************

END
