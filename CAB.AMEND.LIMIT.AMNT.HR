    SUBROUTINE CAB.AMEND.LIMIT.AMNT.HR
************************************************************************************
* 02/07/19  Nuha Tuhul      Copy Of CAB.AMEND.LIMIT.AMNT  with Cond. 114
************************************************************************************

    $INCLUDE T24.BP I_COMMON
    $INCLUDE T24.BP I_EQUATE
    $INCLUDE T24.BP I_F.ACCOUNT
    $INCLUDE T24.BP I_F.LIMIT
    $INCLUDE BP I_F.CAB.LIMIT.MAINT

    LIMIT.ID = '' ; AMT.TYPE = 'I'
    INCREASE.TYPE = 'FULL' ; ACCT.CCY = ''
    MESSAGE.ID = ''
    F.LIMIT = ''
    FN.LIMIT = 'F.LIMIT'
    CALL OPF(FN.LIMIT,F.LIMIT)

    F.ACCOUNT = ''
    FN.ACCOUNT = 'F.ACCOUNT'
    CALL OPF(FN.ACCOUNT,F.ACCOUNT)

    CALL F.READ(FN.ACCOUNT,R.NEW(CAB.LIM.ACCOUNT.NO),R.ACCOUNT,F.ACCOUNT,ACC.ERROR)
    IF FIELD(R.ACCOUNT<AC.LIMIT.REF>,'.',1) NE '114' THEN
        AF = CAB.LIM.ACCOUNT.NO
        ETEXT = 'AC-INVLD.ACCT'
        CALL STORE.END.ERROR
        RETURN
    END ELSE
        LIMIT.ID = R.ACCOUNT<AC.CUSTOMER>:'.':FMT(FIELD(R.ACCOUNT<AC.LIMIT.REF>,'.',1),'R%7'):'.':FIELD(R.ACCOUNT<AC.LIMIT.REF>,'.',2)
        CALL F.READ(FN.LIMIT,LIMIT.ID,LIMIT.REC,F.LIMIT,LIMIT.ERR)
        IF V$FUNCTION EQ 'I' THEN
            R.NEW(CAB.LIM.NEW.LIMIT) = LIMIT.REC<LI.MAXIMUM.TOTAL> - R.NEW(CAB.LIM.DESCREASE.AMOUNT)
            R.NEW(CAB.LIM.CO.CODE) = ID.COMPANY
            IF (R.NEW(CAB.LIM.DESCREASE.AMOUNT) GT LIMIT.REC<LI.MAXIMUM.TOTAL> ) OR (R.NEW(CAB.LIM.DESCREASE.AMOUNT) EQ '' ) OR (LIMIT.REC EQ '' ) THEN
                AF = CAB.LIM.DESCREASE.AMOUNT
                ETEXT = 'AC-INVLD.LMT'
                CALL STORE.END.ERROR
                RETURN
            END
        END
        IF V$FUNCTION EQ 'A' THEN
            ACCT.CCY = R.ACCOUNT<AC.CURRENCY>
            IF R.NEW(CAB.LIM.DESCREASE.AMOUNT) NE '0' THEN
                REDUCED.AMNT = R.NEW(CAB.LIM.DESCREASE.AMOUNT) * -1
            END
            IF R.NEW(CAB.LIM.NEW.LIMIT) EQ '0' THEN
                OFS.SOURCE.ID = 'CABPOS'
                OFS.MSG = 'ACCOUNT,INP/I/PROCESS,//':ID.COMPANY:',':R.NEW(CAB.LIM.ACCOUNT.NO):',LIMIT.REF:1:1=""'
                CALL OFS.POST.MESSAGE(OFS.MSG,MESSAGE.ID,OFS.SOURCE.ID,'')
            END
            CALL LIMIT.PROCESS.INCREASE(LIMIT.ID,'',INCREASE.TYPE,ACCT.CCY,REDUCED.AMNT,AMT.TYPE)
        END
    END
    RETURN
END
