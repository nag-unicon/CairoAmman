    SUBROUTINE AA.GET.LOAN.BALANCE

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.AA.ARRANGEMENT
    $INSERT I_F.ACCT.ACTIVITY
    $INSERT I_F.AA.PROPERTY.CLASS
    $INSERT I_F.AA.OVERDUE

    FN.SAVED.LISTS = "&SAVEDLISTS&"
    F.SAVED.LISTS = ""
    CALL OPF(FN.SAVED.LISTS, F.SAVED.LISTS)

    FN.AA.ARRANGEMENT= "F.AA.ARRANGEMENT"
    F.AA.ARRANGEMENT = ""
    CALL OPF(FN.AA.ARRANGEMENT, F.AA.ARRANGEMENT)

    R.SAVED.LISTS = ""
    ERR.SAVED.LISTS = ""

    PRODUCT.LINE = "LENDING"

    CALL F.READ(FN.SAVED.LISTS, 'CORRECTION.LIST', R.SAVED.LISTS, F.SAVED.LISTS, ERR.SAVED.LISTS)

    GOSUB CHECK.PROPERTY.BALANCE

    CALL F.WRITE(FN.SAVED.LISTS, 'OUTSTANDING.LIST',OUTSTANDING.LIST)
    CALL JOURNAL.UPDATE('')

    RETURN

GET.PROPERTY.CLASS.RECORD:

    CALL CACHE.READ("F.AA.PROPERTY.CLASS", PROPERTY.CLASS, R.PROPERTY.CLASS, RET.ERROR)

    PROD.LINE.POS = 0

    LOCATE PRODUCT.LINE IN R.PROPERTY.CLASS<AA.PC.PRODUCT.LINE,1> SETTING PROD.LINE.POS THEN
        BALANCE.PREFIX.COUNT = DCOUNT(R.PROPERTY.CLASS<AA.PC.BALANCE.PREFIX,PROD.LINE.POS>, SM)
    END

    RETURN
*** </region>

    RETURN

CHECK.PROPERTY.BALANCE:

    LOOP
        REMOVE ARRANGEMENT.ID FROM R.SAVED.LISTS SETTING ARR.POS
    WHILE ARRANGEMENT.ID : ARR.POS

        PROPERTY.ID = ""
        PROPERTY.AMOUNT = ""
        REQUEST.TYPE = ""
        REQUEST.TYPE<2> = "ALL"         ;* Get unauthorised balance amount

        R.AA.ARRANGEMENT = ""
        ERR.AA.ARRANGEMENT = ""
        CALL F.READ(FN.AA.ARRANGEMENT, ARRANGEMENT.ID, R.AA.ARRANGEMENT, F.AA.ARRANGEMENT, ERR.AA.ARRANGEMENT)

        START.DATE = R.AA.ARRANGEMENT<AA.ARR.START.DATE>
        START.DATE = TODAY
        ACCOUNT.ID = R.AA.ARRANGEMENT<AA.ARR.LINKED.APPL.ID>

        PROPERTIES = R.AA.ARRANGEMENT<AA.ARR.PROPERTY,1>
        CHANGE SM TO FM IN PROPERTIES

        PROPERTY.COUNT = DCOUNT(PROPERTIES, FM)

        FOR PROP.CNT = 1 TO PROPERTY.COUNT

            PROPERTY.ID = PROPERTIES<PROP.CNT>    ;*Take all the properties for that property class.

            CALL AA.GET.PROPERTY.CLASS(PROPERTY.ID, PROPERTY.CLASS)

            IF PROPERTY.CLASS MATCHES "ACCOUNT":VM:"INTEREST":VM:"CHARGE" THEN

                GOSUB GET.PROPERTY.CLASS.RECORD   ;* Read the Property Class Record

                FOR BAL.PREFIX.CNT = 1 TO BALANCE.PREFIX.COUNT

                    BALANCE.PREFIX = R.PROPERTY.CLASS<AA.PC.BALANCE.PREFIX,PROD.LINE.POS,BAL.PREFIX.CNT>
                    CALL AA.PROPERTY.GET.BALANCE.NAME(PROPERTY.ID, BALANCE.PREFIX, "", BALANCE.TYPE)

                    IF BALANCE.PREFIX EQ "AGE" THEN
                        GOSUB CHECK.OVERDUE.BALANCE
                    END ELSE
                        GOSUB GET.PROPERTY.AMOUNT
                    END

                NEXT BAL.PREFIX.CNT

            END

        NEXT PROP.CNT

    REPEAT

    RETURN

GET.PROPERTY.AMOUNT:

    IF BALANCE.PREFIX = 'ACC' AND PROPERTY.CLASS EQ "INTEREST" THEN

        ADJUST.REQUEST.TYPE = 'CURRENT'
        ADJUSTMENT.AMOUNT = ""
        CALL AA.GET.ADJUSTED.INTEREST.AMOUNT(ARRANGEMENT.ID, PROPERTY.ID, ADJUST.REQUEST.TYPE, "", EFFECTIVE.DATE, ADJUSTMENT.AMOUNT, RETURN.ERROR)
        PROPERTY.AMT = ADJUSTMENT.AMOUNT
    END ELSE
        CALL AA.GET.PERIOD.BALANCES(ACCOUNT.ID, BALANCE.TYPE, REQUEST.TYPE, START.DATE, "", "", BALANCE.DETAILS, ERROR.MESSAGE)   ;* Get Property amount
        PROPERTY.AMT = ABS(BALANCE.DETAILS<IC.ACT.BALANCE>) ;* Get the Current balance for the balance Type
    END

    IF PROPERTY.AMT THEN

        IF BALANCE.PREFIX = 'ACC' AND PROPERTY.CLASS EQ "CHARGE" THEN
            OUTSTANDING.LIST<-1> = ARRANGEMENT.ID :',': ACCOUNT.ID :',': BALANCE.TYPE :',': PROPERTY.AMT :',': PROPERTY.CLASS :',': "Amortisation"
        END ELSE
            OUTSTANDING.LIST<-1> = ARRANGEMENT.ID :',': ACCOUNT.ID :',': BALANCE.TYPE :',': PROPERTY.AMT :',': PROPERTY.CLASS
        END
    END

    RETURN

CHECK.OVERDUE.BALANCE:

** If the balance prefix is AGE then read the OVERDUE property class and check all the balances

    GOSUB GET.OVERDUE.RECORD

    OVERDUE.STATUS = RAISE(OVERDUE.STATUS)

    CNT =1

    LOOP
    WHILE OVERDUE.STATUS<CNT>

        BALANCE.PREFIX = OVERDUE.STATUS<CNT>
        CALL AA.PROPERTY.GET.BALANCE.NAME(PROPERTY.ID, BALANCE.PREFIX, "", BALANCE.TYPE)

        GOSUB GET.PROPERTY.AMOUNT

        CNT + = 1
    REPEAT

    RETURN

GET.OVERDUE.RECORD:

*** <region name= Overdue Record>
*** <desc>Get property class record for current property </desc>

    CALL AA.GET.PROPERTY.RECORD("", ARRANGEMENT.ID, "", EFFECTIVE.DATE, "OVERDUE","", OVERDUE.RECORD, RET.ERROR)        ;* Get the term amount property record

    OVERDUE.STATUS = OVERDUE.RECORD<AA.OD.OVERDUE.STATUS>

    RETURN
END
