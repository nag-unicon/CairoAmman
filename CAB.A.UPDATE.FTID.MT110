*-----------------------------------------------------------------------------
* <Rating>-60</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE CAB.A.UPDATE.FTID.MT110
*---------------------------------------------------------------
*Company   Name    : Cairo Amman Bank
*Developed By      : Temenos Application Management
*Program   Name    : CAB.A.UPDATE.FTID.MT110
*---------------------------------------------------------------
*Description       :
*Linked With       :
*In  Parameter     : N/A
*Out Parameter     : N/A
*ODR number used   : ODR-2011-10-0067
*---------------------------------------------------------------
*Modification Details:
*=====================
*-----------------------------------------------------------------------------
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.TELLER
    $INSERT I_F.OFS.SOURCE
    $INSERT I_F.FUNDS.TRANSFER
    $INCLUDE CAB.BP I_F.CAB.FILE.PARAMETER
    $INCLUDE CAB.BP I_F.DD.CHQ.TYPE

    IF V$FUNCTION EQ "R" OR R.NEW(TT.TE.RECORD.STATUS) EQ "RNAU" OR R.NEW(TT.TE.RECORD.STATUS) EQ "RNA2" THEN
        RETURN
    END ELSE
        IF R.NEW(TT.TE.AMOUNT.LOCAL.1) GT "1000" THEN
            IF R.NEW(TT.TE.RECORD.STATUS) NE "INA2" THEN
                RETURN
            END
        END
        GOSUB INIT.FILES
        GOSUB OPEN.FILES
        GOSUB PROCESS
    END

    RETURN

***********
INIT.FILES:
***********

    FN.DD = "F.DD.CHQ.TYPE"
    F.DD = ""
    FN.CAB.FILE = "F.CAB.FILE.PARAMETER"
    F.CAB.FILE = ""

    RETURN

***********
OPEN.FILES:
***********

    CALL OPF(FN.DD,F.DD)
    CALL OPF(FN.CAB.FILE,F.CAB.FILE)

    RETURN

********
PROCESS:
********

    LOCAL.APPLICATION = 'TELLER'
    LOCAL.FIELD = 'L.MT110.REF':VM:'L.DD.TYPE':VM:'L.SND.TO.PARTY':VM:'L.BK.TO.BK.OUT':VM:'L.GEN.MT110'
    LOCAL.POS = '' ; Y.OFS.SOURCE.ID = "DD.MC"
    CALL MULTI.GET.LOC.REF(LOCAL.APPLICATION,LOCAL.FIELD,LOCAL.POS)
    L.MT110.REF.POS = LOCAL.POS<1,1>
    L.DD.TYPE.POS = LOCAL.POS<1,2>
    L.SND.TO.PARTY.POS = LOCAL.POS<1,3>
    L.BK.TO.BK.OUT.POS = LOCAL.POS<1,4>
    L.GEN.MT110.POS = LOCAL.POS<1,5>
    IF R.NEW(TT.TE.LOCAL.REF)<1,L.GEN.MT110.POS> NE "YES" THEN
        RETURN
    END
    DD.TYPE = R.NEW(TT.TE.LOCAL.REF)<1,L.DD.TYPE.POS>
    CALL F.READ(FN.DD,DD.TYPE,R.DD,F.DD,DD.ERR)
    IF R.DD THEN
        INT.ACCT = R.DD<DD.CHQ.INTERNAL.ACCOUNT>
    END
    Y.BEN.DETAILS = R.NEW(TT.TE.NARRATIVE.1)
    CHANGE SM TO "" IN Y.BEN.DETAILS
    Y.BK.CNT = DCOUNT(R.NEW(TT.TE.LOCAL.REF)<1,L.BK.TO.BK.OUT.POS>,SM)
    BK.CNT = 1
    LOOP
    WHILE BK.CNT LE Y.BK.CNT
        BK.INFO.ARR<-1> = "BK.TO.BK.OUT:1:":BK.CNT:"=":R.NEW(TT.TE.LOCAL.REF)<1,L.BK.TO.BK.OUT.POS,BK.CNT>
        BK.CNT = BK.CNT + 1
    REPEAT
    CHANGE FM TO "," IN BK.INFO.ARR
    Y.FT.DATA = 'FUNDS.TRANSFER,TT.DD.ISS.ACCT.MT110/I/PROCESS,,,DEBIT.ACCT.NO::=':R.NEW(TT.TE.ACCOUNT.1):',BEN.CUSTOMER::=':Y.BEN.DETAILS:',CREDIT.AMOUNT::=':R.NEW(TT.TE.AMOUNT.FCY.1):','
    Y.FT.DATA:= 'PAYMENT.DETAILS::=':R.NEW(TT.TE.NARRATIVE.2)<1,1>:',ORDERING.CUST::=':R.NEW(TT.TE.NARRATIVE.2)<1,3>:',CREDIT.CURRENCY::=':R.NEW(TT.TE.CURRENCY.1):',CREDIT.ACCT.NO::=':INT.ACCT:','
    Y.FT.DATA:= 'CHEQUE.NUMBER::=':R.NEW(TT.TE.STOCK.NUMBER):',SEND.TO.PARTY::=':R.NEW(TT.TE.LOCAL.REF)<1,L.SND.TO.PARTY.POS>:',':BK.INFO.ARR
    Y.FT.DATA.TEMP = EREPLACE(Y.FT.DATA,'PROCESS','VALIDATE',1,1)
    CALL OFS.GLOBUS.MANAGER(Y.OFS.SOURCE.ID,Y.FT.DATA.TEMP)
    Y.FT.STATUS = FIELD(Y.FT.DATA.TEMP,'/',3)[1,1]
    IF Y.FT.STATUS EQ '1' THEN
        Y.FT.ID = FIELD(Y.FT.DATA.TEMP,'/',1,1)
        CHANGE ',' TO FM IN Y.FT.DATA
        INS Y.FT.ID BEFORE Y.FT.DATA<4>
        DEL Y.FT.DATA<5>
        CHANGE FM TO ',' IN Y.FT.DATA
        R.NEW(TT.TE.LOCAL.REF)<1,L.MT110.REF.POS> = Y.FT.ID
        CALL OFS.POST.MESSAGE(Y.FT.DATA,'',Y.OFS.SOURCE.ID,OPERATOR)
    END ELSE
        CALL F.READ(FN.CAB.FILE,"PAYMENTS",R.CAB.FILE,F.CAB.FILE,CAB.ERR)
        IF R.CAB.FILE THEN
            FILE.PATH = R.CAB.FILE<CAB.FIL.GEN.FILE.PATH>
            FN.FP = FILE.PATH
            F.FP = ""
            CALL OPF(FN.FP,F.FP)
            CALL F.READ(FN.FP,'TB6.ERR.FILE.':TODAY,R.FP,F.FP,FP.ERR)
            IF R.FP THEN
                R.FP<-1> = Y.FT.DATA.TEMP
            END ELSE
                R.FP = Y.FT.DATA.TEMP
            END
            CALL F.WRITE(FN.FP,'TB6.ERR.FILE.':TODAY,R.FP)
        END
    END

    RETURN

END
