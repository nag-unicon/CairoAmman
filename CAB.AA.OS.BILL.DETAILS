    SUBROUTINE CAB.AA.OS.BILL.DETAILS(RET.ARRAY)
***************************************************************************
*** This routine is modify based on the customer request to add new fields
***
***
*** 08-08-2015   Alignment problem routine has been change to display correctly
***
*** 17-02-2019    Nuha Tuhul                Stop Core rtn  AA.GET.BILL.DETAILS
*                                          inseatd read AA.BILL.DETAILS
***************************************************************************
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_ENQUIRY.COMMON
    $INSERT I_F.ENQUIRY
    $INSERT I_F.CUSTOMER
    $INSERT I_F.ACCOUNT
    $INSERT I_F.AA.ACCOUNT
    $INSERT I_F.AA.ARRANGEMENT
    $INSERT I_F.AA.BILL.DETAILS
    $INSERT I_F.AA.ACCOUNT.DETAILS

    GOSUB INIT
    GOSUB GET.INPUT

    IF ENQ.SELECTION<1> NE 'CAB.OS.BILL.DETAILS.REP' THEN

        BEGIN CASE

        CASE BILL.FLG EQ '1'
            GOSUB GET.BILL.DETAILS

        CASE CUS.FLG OR PRO.FLG OR ARR.ID OR COM.FLG
            SORT.BY =' BY PRODUCT BY CO.CODE BY CUSTOMER'
            GOSUB PROCESS

        END CASE

    END ELSE
        SORT.BY =' BY PRODUCT BY CO.CODE BY CUSTOMER'
        GOSUB PROCESS
* GOSUB CONVER.TO.CSV
    END

    RETURN

CONVER.TO.CSV:

    OPEN '','&SAVEDLISTS&' TO F$SAVEDLISTS ELSE
        ERR.OPEN ='EB.RTN.CANT.OPEN.&SAVEDLISTS'
    END

    KEY.FORMAT     = "AA.BILL.DETAILS-":TODAY:".csv"
    FIN.RET.ARRAY  = 'Branch_Name':"*":'Customer_Id':"*":'Customer_Name':"*":'Arrangement_ID':"*":'Loan_Account_No':"*":'Curency':'Loan_Balance':
    FIN.RET.ARRAY := "*":'Original_Amount':"*":'Available_Amount':"*":'Bill_Reference':"*":'Bill_Status':"*":'Bill_Date':"*":'Original_Bill_Amount'
    FIN.RET.ARRAY := "*":'Outstanding_Bill_Amount':"*":'Accrual_Status':"*":'Overdue_Status':"*":'Product_Name':"*":'Disbursement_Date':"*" :'Purpose_of_Facility'
*    FIN.RET.ARRAY := "*":'No._of_future_schedules':"*":'Interest_Rate':"*":'Legal_Status':"*":'Settlement_Account_No.':"*":'Customer_Status':"*"
    FIN.RET.ARRAY := "*":'Interest_Rate':"*":'Legal_Status':"*":'Settlement_Account_No.':"*":'Customer_Status':"*"
    FIN.RET.ARRAY := 'Category':"*":'Officer'

    FIN.RET.ARRAY<-1> = RET.ARRAY
    CONVERT '*' TO ','IN FIN.RET.ARRAY

    WRITE FIN.RET.ARRAY TO F$SAVEDLISTS,KEY.FORMAT

    RETURN

INIT:

    FN.ACCOUNT = 'F.ACCOUNT'
    F.ACCOUNT  = ''
    CALL OPF(FN.ACCOUNT, F.ACCOUNT)

    FN.CUSTOMER = 'F.CUSTOMER'
    F.CUSTOMER  = ''
    CALL OPF(FN.CUSTOMER,F.CUSTOMER)

    FN.ARRANGEMENT = 'F.AA.ARRANGEMENT'
    F.ARRANGEMENT  = ''
    CALL OPF(FN.ARRANGEMENT,F.ARRANGEMENT)

    APP.NAME   = 'AA.ARR.ACCOUNT' :FM: 'ACCOUNT'
    FIELD.NAME = 'SETTLE.ACCOUNT':VM:'PURPOSE.FACILTY':VM:'LEGAL.STATUS':FM:'PURPOSE.FACILTY':VM:'L.AGING.STATUS'
    FIELD.POS  = '';
    CALL MULTI.GET.LOC.REF(APP.NAME,FIELD.NAME,FIELD.POS)
    SET.POS     = FIELD.POS<1,1>
    PUR.POS     = FIELD.POS<1,2>
    LEG.POS     = FIELD.POS<1,3>
    ACC.PUR.POS = FIELD.POS<2,1>
    ACC.OVER.POS= FIELD.POS<2,2>

    FN.AA.BILL.DETAILS = "F.AA.BILL.DETAILS"
    F.AA.BILL.DETAILS = ""
    CALL OPF(FN.AA.BILL.DETAILS, F.AA.BILL.DETAILS)

    FN.AA.BILL.DETAILS.HIST = "F.AA.BILL.DETAILS.HIST"
    F.AA.BILL.DETAILS.HIST = ""
    CALL OPF(FN.AA.BILL.DETAILS.HIST, F.AA.BILL.DETAILS.HIST)


    RETURN

GET.INPUT:

    CUS.FLG = ''; PRO.FLG = '' ;COM.FLG = '';ARR.ID = ''; BILL.FLG = ''
    EFF.DATE = TODAY ; END.DT = TODAY;

    SEL.TEXT = ' WITH PRODUCT.LINE EQ LENDING AND (ARR.STATUS EQ EXPIRED OR ARR.STATUS EQ CURRENT)'

    LOCATE 'CUSTOMER' IN ENQ.SELECTION<2,1> SETTING CUS.POS THEN
        CUS.NO = ENQ.SELECTION<4,CUS.POS>
        SEL.TEXT<-1> = ' AND CUSTOMER EQ ': CUS.NO
        CUS.FLG = '1'
    END

    LOCATE 'PRODUCT' IN ENQ.SELECTION<2,1> SETTING PRO.POS THEN
        PROD.ID = ENQ.SELECTION<4,PRO.POS>
        SEL.TEXT<-1> = ' AND PRODUCT EQ ' : PROD.ID
        PRO.FLG ='1'
    END

    LOCATE 'COMPANY' IN ENQ.SELECTION<2,1> SETTING COM.POS THEN
        COM.ID = ENQ.SELECTION<4,COM.POS>
        SEL.TEXT<-1> = ' AND CO.CODE EQ ' : COM.ID
        COM.FLG = '1'
    END

    LOCATE 'ARRANGEMENT.ID' IN ENQ.SELECTION<2,1> SETTING ARR.POS THEN
        ARR.ID = ENQ.SELECTION<4,ARR.POS>
        SEL.TEXT <-1> = ' AND @ID EQ ': ARR.ID
        ARR.ID ='1'
    END

    LOCATE 'BILL.ID' IN ENQ.SELECTION<2,1> SETTING BILL.SEL.POS THEN
        BILL.ID = ENQ.SELECTION<4,BILL.SEL.POS>
        BILL.FLG = '1'
    END

    CONVERT FM TO ' ' IN SEL.TEXT

    RETURN

PROCESS:

    SEL.LIST = ''
    SEL.CMD  = 'SELECT ': FN.ARRANGEMENT : SEL.TEXT : SORT.BY

    CALL EB.READLIST(SEL.CMD, SEL.LIST, '', NO.OF.REC, ERR)

    LOOP
        REMOVE ARR.ID FROM SEL.LIST SETTING ARR.POS
    WHILE ARR.ID : ARR.POS

        BILL.REFERENCES = ''
        BILL.CREATE.DATE = "" ;* Bill creation date can be anything
        BILL.REFERENCES = ""
        BILL.STATUS = "DUE":VM:"AGING"
        BILL.TYPE = ""

        PROCESS.TYPE = "INITIALISE"
        R.AA.ACCOUNT.DETAILS = ""
        CALL AA.PROCESS.ACCOUNT.DETAILS(ARR.ID, PROCESS.TYPE, UPDATE.TYPE, R.AA.ACCOUNT.DETAILS, RET.ERROR)

        CALL AA.GET.BILL(ARR.ID, "", LAST.PAYMENT.DATE, BILL.CREATE.DATE, BILL.TYPE, "", BILL.STATUS, "", "", "", "", BILL.REFERENCES, RET.ERROR)
* CALL AA.GET.BILL(ARR.ID, '','','','','','','UNPAID', '','','',BILL.REFERENCES, RET.ERROR)

        IF BILL.REFERENCES THEN
            GOSUB GET.BILL.DETAILS
        END

    REPEAT

    RETURN

GET.BILL.DETAILS:

    INIT.FLG = '1' ; R.AA.ARRANGEMENT = ''; PRODUCT.ID = ''; PROPERTY.LIST = '';
    CALL AA.GET.ARRANGEMENT.PRODUCT(ARR.ID,EFF.DATE,R.AA.ARRANGEMENT,PRODUCT.ID,PROPERTY.LIST)

    ACC.STATUS     = R.AA.ACCOUNT.DETAILS<AA.AD.SUSP.STATUS,1>
    LOAN.OD.STATUS = R.AA.ACCOUNT.DETAILS<AA.AD.ARR.AGE.STATUS>

    GOSUB GET.ACCT.DETAILS
    GOSUB CONVERT.INTEREST.RATE
    GOSUB GET.ACCT.CONDITIONS
    GOSUB GET.ACCT.BALANCES
    GOSUB GET.ACCURED.BALANCES
*    GOSUB CHECK.FUT.SCH ; * No need to display the no. of future schedules in the enquiry.

    LOOP
        REMOVE BILL.REF FROM BILL.REFERENCES SETTING BILL.POS
    WHILE BILL.REF : BILL.POS

* CALL AA.GET.BILL.DETAILS(ARR.ID,BILL.REF,BILL.DET,BILL.ERR)

        GOSUB NEW.GET.BILL.DETAILS

        BILL.STATUS = R.BILL.DETAILS<32,1>
        BILL.DATE   = R.BILL.DETAILS<3>
        BILL.OR.AMT = R.BILL.DETAILS<6>
        BILL.OS.AMT = R.BILL.DETAILS<10>
        SIM.REF    = ''
        CYCLE.DATE = ''

        IF INIT.FLG EQ '1' THEN
            GOSUB UPDATE.DATA
        END ELSE
            GOSUB ADD.DATA
        END

        INIT.FLG = ''

    REPEAT

    RETURN

NEW.GET.BILL.DETAILS:

    ERR.MSG = ""
    CALL F.READ(FN.AA.BILL.DETAILS, BILL.REF, R.BILL.DETAILS, F.AA.BILL.DETAILS, ERR.MSG)

    IF ERR.MSG THEN ;** Get if from .HIST file

        ERR.MSG = ""
        CALL F.READ(FN.AA.BILL.DETAILS.HIST, BILL.REF, R.BILL.DETAILS, F.AA.BILL.DETAILS.HIST, ERR.MSG)
    END

    RETURN

CONVERT.INTEREST.RATE:

    RET.INT.RATE = '';
    CALL CAB.GET.INTEREST.RATE(ARR.ID,PRIN.INT.RATE,PEN.INT.RATE)
    RET.INT.RATE = PRIN.INT.RATE<1>     ;*** Change to display the interest rate alone
*    CALL AAL.GET.INTEREST.RATE(ARR.ID,'PRINCIPALINT',RET.INT.RATE)


    RETURN

CHECK.FUT.SCH:

    NO.OF.FS = ""
    IF R.AA.ARRANGEMENT<AA.ARR.ARR.STATUS> EQ "CURRENT" THEN          ;* EXPIRED arrangement doesnot required to project since there wont be any future schedule.
        SIM.REF = ''; CYCLE.DATE = ''; NO.OF.DAYS = '';DUE.DATES = ''; DUE.TYPE.AMTS = '' ; DUE.PROPS = '' ;
        CYCLE.DATE = '';TOT.PAYMENT = '';DUE.TYPES = '';DUE.METHODS = ''; DUE.PROP.AMTS = ''; DUE.OUTS = '';
        CYCLE.DATE<1> = TODAY

*       CALL AA.SCHEDULE.PROJECTOR(ARR.ID, SIM.REF, "",CYCLE.DATE, TOT.PAYMENT, DUE.DATES, DUE.TYPES, DUE.METHODS, DUE.TYPE.AMTS, DUE.PROPS, DUE.PROP.AMTS, DUE.OUTS)

        NO.OF.DAYS = DCOUNT(DUE.DATES,FM)
        DAT.POS = '' ;
        LOCATE TODAY IN DUE.DATES BY 'AR' SETTING DAT.POS THEN
            NO.OF.FS = NO.OF.DAYS - DAT.POS
        END ELSE
            NO.OF.FS = NO.OF.DAYS - ( DAT.POS - 1)
        END
    END

    RETURN

GET.ACCURED.BALANCES:

    BALANCE.TYPE = 'UNC': "ACCOUNT"
    GOSUB GET.BALANCES
    U.COMMIT = BALANCE.AMT

    BALANCE.TYPE = 'ACCPRINCIPALINT'
    GOSUB GET.BALANCES
    PRIN.ACC = BALANCE.AMT

    BALANCE.TYPE = 'ACCPENALTYINT'
    GOSUB GET.BALANCES
    PEN.ACC = BALANCE.AMT

    RETURN

GET.ACCT.BALANCES:

    TERM.AMOUNT.PROPERTY = "COMMITMENT"
    BALANCE.TYPE = "TOT" : TERM.AMOUNT.PROPERTY
    GOSUB GET.BALANCES
    T.COMMIT = BALANCE.AMT

    BALANCE.TYPE = "CUR": TERM.AMOUNT.PROPERTY
    GOSUB GET.BALANCES
    C.COMMIT = BALANCE.AMT

    RETURN

GET.BALANCES:

    BAL.DETAILS = ""
    BALANCE.AMT = ""
    RET.ERR = ""

*   CALL AA.GET.PERIOD.BALANCES(ACC.NO,BALANCE.TYPE,REQUEST.TYPE,DIS.DATE,END.DT,'',BAL.DETAILS,RET.ERR)
* BALANCE.AMT = BAL.DETAILS<4>
* BAL.CNT = DCOUNT(BALANCE.AMT, VM)
* BALANCE.AMT = ABS(BALANCE.AMT<1,BAL.CNT>)
    REQUEST.DATE = TODAY
    BALANCE.AMOUNT = ""
    CALL AA.GET.ECB.BALANCE.AMOUNT(ACC.NO, BALANCE.TYPE, REQUEST.DATE, BALANCE.AMT,RET.ERROR)
    BALANCE.AMT = ABS(BALANCE.AMT)
    BALANCE.AMT = FMT(BALANCE.AMT, "R3#19")

    RETURN

GET.ACCT.CONDITIONS:

    CALL AA.GET.ARRANGEMENT.CONDITIONS(ARR.ID,'ACCOUNT','',EFF.DATE,R.ACCOUNT.ID,R.ACCOUNT.COND,ACCT.ERR)

    R.ACCOUNT.COND = RAISE(R.ACCOUNT.COND)
    AC.SET     = R.ACCOUNT.COND<AA.AC.LOCAL.REF><1,SET.POS>
    AC.PUR.FAC = R.ACCOUNT.COND<AA.AC.LOCAL.REF><1,PUR.POS>
    LEG.STATUS = R.ACCOUNT.COND<AA.AC.LOCAL.REF><1,LEG.POS>

    BALANCE.TYPE = "CUR": "ACCOUNT"
    GOSUB GET.BALANCES
    C.ACCOUNT = BALANCE.AMT

    RETURN

GET.ACCT.DETAILS:

    CUS      = R.AA.ARRANGEMENT<AA.ARR.CUSTOMER>
    CURR     = R.AA.ARRANGEMENT<AA.ARR.CURRENCY>
    CO.CODE  = R.AA.ARRANGEMENT<AA.ARR.CO.CODE>
    ACC.NO   = R.AA.ARRANGEMENT<AA.ARR.LINKED.APPL.ID>
    DIS.DATE = R.AA.ACCOUNT.DETAILS<AA.AD.START.DATE>
    MAT.DATE = R.AA.ACCOUNT.DETAILS<AA.AD.MATURITY.DATE>

    CALL F.READ(FN.ACCOUNT,ACC.NO,R.ACC.DETAILS,F.ACCOUNT,ACC.ERR)
    OFFICER = R.ACC.DETAILS<AC.ACCOUNT.OFFICER>
    CATE    = R.ACC.DETAILS<AC.CATEGORY>

    CALL F.READ(FN.CUSTOMER,CUS,R.CUS.DETAILS,F.CUSTOMER,CUS.ERR)
    CUS.NAME   = R.CUS.DETAILS<EB.CUS.SHORT.NAME,1>
    CUS.STATUS = R.CUS.DETAILS<EB.CUS.CUSTOMER.STATUS>

    RETURN

UPDATE.DATA:

    IF CATE NE '1053' THEN
        RET.ARRAY<-1> = CO.CODE : "*":CUS:"*":CUS.NAME:"*":  ARR.ID : "*": ACC.NO : "*":CURR:"*":C.ACCOUNT :"*":T.COMMIT:"*":C.COMMIT:"*":BILL.REF
        RET.ARRAY := "*":BILL.STATUS:"*":BILL.DATE:"*":BILL.OR.AMT:"*":BILL.OS.AMT : "*" :ACC.STATUS : "*":LOAN.OD.STATUS :"*":PRODUCT.ID:"*":DIS.DATE
        RET.ARRAY := "*":AC.PUR.FAC:"*":NO.OF.FS:"*":RET.INT.RATE:"*":LEG.STATUS:"*":AC.SET:"*":CUS.STATUS:"*":CATE:"*":OFFICER : "*":PRIN.ACC : "*": PEN.ACC : "*" : U.COMMIT
    END

    RETURN

ADD.DATA:

    IF CATE NE '1053' THEN
        RET.ARRAY<-1> = "*":"*":"*":"*":"*":"*":"*":"*":"*":BILL.REF:"*":BILL.STATUS:"*":BILL.DATE:"*":BILL.OR.AMT:"*":BILL.OS.AMT : "*" :ACC.STATUS
        RET.ARRAY:= "*":LOAN.OD.STATUS :"*":"*":"*":"*":"*":"*":"*":"*":"*":"*": "*":PRIN.ACC : "*": PEN.ACC : "*" : U.COMMIT
    END

    RETURN
END
