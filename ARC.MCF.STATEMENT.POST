    SUBROUTINE ARC.MCF.STATEMENT.POST
    $INCLUDE T24.BP I_COMMON
    $INCLUDE T24.BP I_EQUATE
    $INCLUDE T24.BP I_F.ARCHIVE
    $INCLUDE T24.BP I_F.DATES
    $INCLUDE CAB.BP I_ARC.MCF.STATEMENT.COMMON

    GOSUB INITIALISE
    GOSUB PROCESS

    RETURN

INITIALISE:

    YR.ACCOUNT = BATCH.DETAILS<3,1>

    R.ARCHIVE = ''
    ARCHIVE.ERR = ''
    CALL CACHE.READ("F.ARCHIVE","STATEMENT",R.ARCHIVE,ARCHIVE.ERR)
    PURGE.DATE = R.ARCHIVE<ARC.PURGE.DATE>

    IF PURGE.DATE = '' THEN

        IF R.ARCHIVE<ARC.RETENTION.PERIOD>[1] = 'M' THEN
            START.YEAR = R.DATES(EB.DAT.TODAY)[1,4]
            START.MONTH = R.DATES(EB.DAT.TODAY)[5,2]
            MONTHS = (START.YEAR * 12) + START.MONTH - R.ARCHIVE<ARC.RETENTION.PERIOD>[1,2]
            START.YEAR = INT(MONTHS/12)
            START.MONTH = REM(MONTHS,12)
            IF START.MONTH = 0 THEN
                START.MONTH = 12
                START.YEAR -= 1
            END
            PURGE.DATE = START.YEAR:FMT(START.MONTH,'2"0"R'):'01'
        END ELSE
            START.YEAR = R.DATES(EB.DAT.TODAY)[1,4]
            START.YEAR -= R.ARCHIVE<ARC.RETENTION.PERIOD>[1,2]
            PURGE.DATE = START.YEAR:'0101'
        END
    END
    FN.ACCT.STMT.PRINT = 'F.ACCT.STMT.PRINT'
    F.ACCT.STMT.PRINT = ''
    CALL OPF(FN.ACCT.STMT.PRINT,F.ACCT.STMT.PRINT)

    FN.ACCT.STMT.PRINT$ARC = 'F.ACCT.STMT.PRINT$ARC'
    FN.ACCT.STMT.PRINT$ARC<2> = 'NO.FATAL.ERROR'
    F.ACCT.STMT.PRINT$ARC = ''
    ETEXT = ''
    CALL OPF(FN.ACCT.STMT.PRINT$ARC,F.ACCT.STMT.PRINT$ARC)

    IF ETEXT THEN
        FILE.TO.OPEN = FN.ACCT.STMT.PRINT$ARC
        CALL EBS.CREATE.FILE(FILE.TO.OPEN,'',ARC.ERR)
    END


    FN.STMT.PRINTED = 'F.STMT.PRINTED'
    F.STMT.PRINTED = ''
    CALL OPF(FN.STMT.PRINTED,F.STMT.PRINTED)

    FN.STMT.PRINTED$ARC = 'F.STMT.PRINTED$ARC'
    FN.STMT.PRINTED$ARC<2> = 'NO.FATAL.ERROR'
    F.STMT.PRINTED$ARC = ''
    ETEXT = ''
    CALL OPF(FN.STMT.PRINTED$ARC,F.STMT.PRINTED$ARC)
    IF ETEXT THEN
        FILE.TO.OPEN = FN.STMT.PRINTED$ARC
        CALL EBS.CREATE.FILE(FILE.TO.OPEN,'',ARC.ERR)
    END

    ACCT.PURGE.DATE = PURGE.DATE
    RETURN

PROCESS:
    IF R.ARCHIVE<ARC.ARCHIVE.DATA> NE "Y" THEN
        RETURN
    END
    R.ACCT.STMT.PRINT = ''
    ACCT.STMT.ERR = ''
    CALL F.READU(FN.ACCT.STMT.PRINT,YR.ACCOUNT,R.ACCT.STMT.PRINT,F.ACCT.STMT.PRINT,ACCT.STMT.ERR,'')
    CALL F.READ(FN.ACCT.STMT.PRINT$ARC,YR.ACCOUNT,R.ACCT.STMT.PRINT$ARC,F.ACCT.STMT.PRINT$ARC,ACCT.STMT.ERR.ARC)
    R.ACCT.STMT.PRINT.DEL = R.ACCT.STMT.PRINT
    STMT.POS = 1
    STMT.DEL = 1
    LOOP
    UNTIL (R.ACCT.STMT.PRINT<STMT.POS> EQ '')
        YR.ACCT.DATE = R.ACCT.STMT.PRINT<STMT.POS>
        YR.ACCT.STMT.DATE = FIELD(YR.ACCT.DATE,'/',1)
        IF YR.ACCT.STMT.DATE LT PURGE.DATE THEN
            YR.ID = YR.ACCOUNT:"-":YR.ACCT.STMT.DATE
            GOSUB DELETE.STMT.PRINTED
            R.ACCT.STMT.PRINT$ARC<-1> = R.ACCT.STMT.PRINT<STMT.POS>
            R.ACCT.STMT.PRINT.DEL = DELETE(R.ACCT.STMT.PRINT.DEL,STMT.DEL) 
        END
        STMT.POS+=1
    REPEAT

    IF R.ACCT.STMT.PRINT$ARC NE '' THEN
        CALL F.WRITE(FN.ACCT.STMT.PRINT$ARC,YR.ACCOUNT,R.ACCT.STMT.PRINT$ARC)
        CALL F.WRITE(FN.ACCT.STMT.PRINT,YR.ACCOUNT,R.ACCT.STMT.PRINT.DEL)
        RELEASE F.ACCT.STMT.PRINT,YR.ACCOUNT
    END

    RETURN

DELETE.STMT.PRINTED:
    R.STMT.PRINTED = ''
    STMT.PRINTED.ERR = ''
    CHECK.STMT.PRINTED.ID = YR.ID
    CALL F.READ(FN.STMT.PRINTED,CHECK.STMT.PRINTED.ID,R.STMT.PRINTED,F.STMT.PRINTED,STMT.PRINTED.ERR)
    IF R.STMT.PRINTED THEN
        CALL F.WRITE(FN.STMT.PRINTED$ARC,CHECK.STMT.PRINTED.ID,R.STMT.PRINTED)
        CALL F.DELETE(FN.STMT.PRINTED,CHECK.STMT.PRINTED.ID)
    END
    RETURN
