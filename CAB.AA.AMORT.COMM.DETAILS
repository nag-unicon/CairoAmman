    SUBROUTINE CAB.AA.AMORT.COMM.DETAILS(RET.ARRAY)
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_ENQUIRY.COMMON
    $INSERT I_F.AA.ACCOUNT
    $INSERT I_F.EB.ACCRUAL
    $INSERT I_F.CUSTOMER

    GOSUB INIT
    GOSUB GET.INP.DATA

    IF ENQ.SELECTION<1> EQ 'CAB.AA.AMORT.DETAILS' THEN
        IF CO.FLG OR CUS.FLG OR ARR.FLG OR PRO.CAT THEN
            GOSUB PROCESS
        END
    END ELSE
        GOSUB PROCESS
        GOSUB CONVERT.TO.CSV
    END
    RETURN

INIT:

    FN.EB.ACC= 'F.EB.ACCRUAL'
    F.EB.ACC = ''
    CALL OPF(FN.EB.ACC,F.EB.ACC)

    FN.CUS = 'F.CUSTOMER'
    F.CUS= ''
    CALL OPF(FN.CUS,F.CUS)

    APP.NAME = 'AA.ARR.ACCOUNT'
    FIELD.NAME='SETTLE.ACCOUNT'
    FIELD.POS = '';
    CALL MULTI.GET.LOC.REF(APP.NAME,FIELD.NAME,FIELD.POS)
    SET.POS = FIELD.POS<1,1>

    EFF.DATE = TODAY

    RETURN

GET.INP.DATA:
    SEL.RUN.INP = ''
    LOCATE 'COMPANY.ID' IN ENQ.SELECTION<2,1> SETTING CO.POS THEN
        CO.CODE = ENQ.SELECTION<4,CO.POS>
        CO.FLG = '1'
        SEL.RUN.INP = ' WITH CO.CODE EQ ' : CO.CODE
    END

    LOCATE 'CUSTOMER' IN ENQ.SELECTION<2,1> SETTING CUS.POS THEN
        CUS.ID = ENQ.SELECTION<4,CUS.POS>
        CUS.FLG = '1'

        IF SEL.RUN.INP THEN SEL.RUN.INP<-1> = ' AND CUSTOMER EQ ' : CUS.ID
        ELSE SEL.RUN.INP = ' WITH CUSTOMER EQ ' : CUS.ID

    END

    LOCATE 'ARRANGEMENT.ID' IN ENQ.SELECTION<2,1> SETTING ARR.POS THEN
        ARR.ID = ENQ.SELECTION<4,ARR.POS>
        ARR.FLG = '1'

        IF SEL.RUN.INP THEN SEL.RUN.INP<-1> = ' AND @ID LIKE ' : ARR.ID : "..."
        ELSE SEL.RUN.INP = ' WITH @ID LIKE ' : ARR.ID : '...'

    END

    LOCATE 'PRODUCT.CAT' IN ENQ.SELECTION<2,1> SETTING PRO.POS THEN
        PRO.CAT = ENQ.SELECTION<4,PRO.POS>
        PRO.FLG = '1'
        IF SEL.RUN.INP THEN SEL.RUN.INP<-1> = ' AND PRODUCT.CATEGORY EQ ' : PRO.CAT
        ELSE SEL.RUN.INP = ' WITH PRODUCT.CATEGORY EQ ' : PRO.CAT
    END

    CONVERT FM TO ' ' IN SEL.RUN.INP
    RETURN

PROCESS:
    SEL.CMD = 'SELECT ' : FN.EB.ACC : SEL.RUN.INP : ' BY PRODUCT.CATEGORY BY CO.CODE BY CUSTOMER'
    SEL.LIST = ''
    CALL EB.READLIST(SEL.CMD,SEL.LIST,'',NO.OF.REC,SEL.ERR)
    LOOP
        REMOVE REC.ID FROM SEL.LIST SETTING SEL.POS
    WHILE REC.ID : SEL.POS
        R.ACC.DET = '';
        CALL F.READ(FN.EB.ACC,REC.ID,R.ACC.DET,F.EB.ACC,EB.ACC.ERR)
        ARR.ID = FIELD(REC.ID,'*',1)
        ACC.NO = R.ACC.DET<EB.ACC.CONTRACT.BAL.ID>
        CUS.NO = R.ACC.DET<EB.ACC.CUSTOMER>
        CATE = R.ACC.DET<EB.ACC.PRODUCT.CATEGORY>
        OFFICER = R.ACC.DET<EB.ACC.ACCOUNT.OFFICER>
        CO.CODE = R.ACC.DET<EB.ACC.CO.CODE>
        CRG.CCY = R.ACC.DET<EB.ACC.CHARGE.CCY>
        CRG.OS.AMT = R.ACC.DET<EB.ACC.CHARGE.AMT.LCY>
        PL.CATE  = R.ACC.DET<EB.ACC.PL.CATEGORY>
        ACC.FREQ = R.ACC.DET<EB.ACC.FREQUENCY>
        AMT.PERIOD = R.ACC.DET<EB.ACC.PERIOD>
        ACC.ST = R.ACC.DET<EB.ACC.START.DATE>
        ACC.END =R.ACC.DET<EB.ACC.END.DATE>
        ACC.AMT =R.ACC.DET<EB.ACC.OTS.AMOUNT>
        UNACC.AMT = CRG.OS.AMT - ACC.AMT
        REP.TYPE = R.ACC.DET<EB.ACC.CRF.TYPE>

        GOSUB GET.CUST.DETAILS
        GOSUB GET.SETT.DETAILS
        RET.ARRAY <-1> = CO.CODE : "*" : PROD.ID :"*":CATE:"*":OFFICER : "*":ARR.ID:"*": ACC.NO:"*":CUS.NO :"*":CUS.NAME:"*":AC.SET :"*":CUS.STATUS : "*":CRG.CCY:"*":CRG.OS.AMT
        RET.ARRAY := "*":ACC.AMT:"*":UNACC.AMT:"*":ACC.FREQ:"*":ACC.ST:"*":ACC.END:"*":AMT.PERIOD:"*":PL.CATE:"*":REP.TYPE
    REPEAT


    RETURN
CONVERT.TO.CSV:

    OPEN '','&SAVEDLISTS&' TO F$SAVEDLISTS ELSE
        ERR.OPEN ='EB.RTN.CANT.OPEN.&SAVEDLISTS'
    END

    KEY.FORMAT = "AA.AMORT.DETAILS-":TODAY:".csv"
    FIN.RET.ARRAY <-1>= 'Branch_Name':"*":'Product_Name':"*":'Category':"*":'Officer':"*":'Arrangement_Id':"*":'Loan_Account_No':"*":'Customer_No':"*"
    FIN.RET.ARRAY:='Customer_Name':"*":'Settlement_Account_No':"*":'Customer_Status':"*":'Charge_Currency':"*":'Original _Charge_Amount':"*":
    FIN.RET.ARRAY:= 'Amortised_Charge_Amount-Accrued':"*":'uNACCURED':"*":'Amortisation_Frequency':"*":'Start_Date_of_Accrual':"*": 'End_Date_of_Accrual'
    FIN.RET.ARRAY:= "*":'Amortisation_Period':"*":'P&L_Category':"*":'Reporting_Type'

    FIN.RET.ARRAY<-1> = RET.ARRAY
    CONVERT '*' TO ','IN FIN.RET.ARRAY
    WRITE FIN.RET.ARRAY TO F$SAVEDLISTS,KEY.FORMAT
    RETURN

GET.SETT.DETAILS:
    R.ACCOUNT.COND = '';
    CALL AA.GET.ARRANGEMENT.CONDITIONS(ARR.ID,'ACCOUNT','',EFF.DATE,R.ACCOUNT.ID,R.ACCOUNT.COND,ACCT.ERR)
    R.ACCOUNT.COND = RAISE(R.ACCOUNT.COND)
    AC.SET = R.ACCOUNT.COND<AA.AC.LOCAL.REF><1,SET.POS>

    RETURN
GET.CUST.DETAILS:
    CUS.STATUS = '' ; CUS.NAME = ''; R.CUS.DET = '';
    CALL F.READ(FN.CUS,CUS.NO,R.CUS.DET,F.CUS,CUS.ERR)
    CUS.STATUS = R.CUS.DET<EB.CUS.CUSTOMER.STATUS>
    CUS.NAME = R.CUS.DET<EB.CUS.SHORT.NAME,1>
    ARR.DETAILS = '' ; PROD.ID = ''; PROP.LIST = '';
    CALL AA.GET.ARRANGEMENT.PRODUCT(ARR.ID,EFF.DATE,ARR.DETAILS,PROD.ID,PROP.LIST)
    RETURN
END
