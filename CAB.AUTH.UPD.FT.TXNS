    SUBROUTINE CAB.AUTH.UPD.FT.TXNS

    $INCLUDE T24.BP I_COMMON
    $INCLUDE T24.BP I_EQUATE
    $INCLUDE T24.BP I_F.TELLER
    $INCLUDE T24.BP I_F.FUNDS.TRANSFER
    $INCLUDE CAB.BP I_F.CAB.FT.TXNS.LOG
    $INCLUDE T24.BP I_GTS.COMMON
    $INCLUDE T24.BP I_F.FT.TXN.TYPE.CONDITION
    $INCLUDE T24.BP I_F.FT.COMMISSION.TYPE
    $INCLUDE T24.BP I_F.ACCOUNT


    IF OFS$SOURCE.ID EQ 'BROWSERTC' THEN
        GOSUB INIT
        IF APPLICATION EQ 'FUNDS.TRANSFER' THEN
            GOSUB PROCESS.FT
        END
    END

    RETURN

*****
INIT:
*****

    FN.CAB.FT.TXNS.LOG = 'F.CAB.FT.TXNS.LOG'
    F.CAB.FT.TXNS.LOG = ''
    CALL OPF(FN.CAB.FT.TXNS.LOG,F.CAB.FT.TXNS.LOG)

    Y.ACCT.ID.DES = ''
    TRXN.REC = ''
    YR.LOC.REF.POS = ''
    Y.ACCT.ID.DES = ''
    YR.LOC.REF.FIELDS = "DEAL.REF":VM:"LEGACY.ACCT":VM:"PAY.DET.ID"
    CALL MULTI.GET.LOC.REF("FUNDS.TRANSFER",YR.LOC.REF.FIELDS,YR.LOC.REF.POS)
    DEAL.REF.POS   = YR.LOC.REF.POS<1,1>
    LEGACY.ACCT.POS = YR.LOC.REF.POS<1,2>
    PAY.DET.ID.POS = YR.LOC.REF.POS<1,3>
    Y.ACCT = ''
    Y.CATEGORY = ''

    RETURN

***********
PROCESS.FT:
***********
    IF (V$FUNCTION EQ 'R' ) OR (R.NEW(FT.RECORD.STATUS) EQ 'RNAU' ) THEN
        CALL F.DELETE(FN.CAB.FT.TXNS.LOG,ID.NEW)
        RETURN
    END

    TRXN.REC<CAB.FTLOG.CO.CODE> = ID.COMPANY
    TRXN.REC<CAB.FTLOG.TXN.DATE> =  TODAY
    TRXN.REC<CAB.FTLOG.TXN.DATE.TIME> =  R.NEW(FT.DATE.TIME)
    TRXN.REC<CAB.FTLOG.RECORD.STATUS> =  R.NEW(FT.RECORD.STATUS)
    TRXN.REC<CAB.FTLOG.INPUTTER> =  FIELD(R.NEW(FT.INPUTTER),'_',2)
    TRXN.REC<CAB.FTLOG.AUTHORISER> =  OPERATOR

    TRXN.REC<CAB.FTLOG.TRANSACTION.TYPE> = R.NEW(FT.TRANSACTION.TYPE)
    GOSUB GET.TRANSACTION.DESC

    TRXN.REC<CAB.FTLOG.TXN.DISCRIPION> = R.FT.TXN.TYPE.CONDITION<FT6.DESCRIPTION>
    TRXN.REC<CAB.FTLOG.DEBIT.ACCT.NO> = R.NEW(FT.DEBIT.ACCT.NO)
    TRXN.REC<CAB.FTLOG.DEBIT.CURRENCY> = R.NEW(FT.DEBIT.CURRENCY)
    TRXN.REC<CAB.FTLOG.DEBIT.AMOUNT> =   R.NEW(FT.DEBIT.AMOUNT)

    YR.ACCT  = R.NEW(FT.DEBIT.ACCT.NO)
    GOSUB GET.ACCOUNT.NAME
    TRXN.REC<CAB.FTLOG.DR.CATEGORY> =  Y.CATEGORY
    TRXN.REC<CAB.FTLOG.DEBIT.ACCT.NAME> = Y.ACCT.ID.DES

    TRXN.REC<CAB.FTLOG.AMOUNT.DEBITED> =  R.NEW(FT.AMOUNT.DEBITED)
    TRXN.REC<CAB.FTLOG.DEBIT.VALUE.DATE> = R.NEW(FT.DEBIT.VALUE.DATE)
    TRXN.REC<CAB.FTLOG.DEBIT.THEIR.REF> =  R.NEW(FT.DEBIT.THEIR.REF)
    TRXN.REC<CAB.FTLOG.ORDERING.CUST> =  R.NEW(FT.ORDERING.CUST)
    TRXN.REC<CAB.FTLOG.DEAL.REF> =   R.NEW(FT.LOCAL.REF)<1,DEAL.REF.POS>
    TRXN.REC<CAB.FTLOG.LEGACY.ACCT> =  R.NEW(FT.LOCAL.REF)<1,LEGACY.ACCT.POS>
    TRXN.REC<CAB.FTLOG.CREDIT.ACCT.NO> =  R.NEW(FT.CREDIT.ACCT.NO)
    TRXN.REC<CAB.FTLOG.CREDIT.CURRENCY> =  R.NEW(FT.CREDIT.CURRENCY)
    TRXN.REC<CAB.FTLOG.CREDIT.AMOUNT> =  R.NEW(FT.CREDIT.AMOUNT)

    YR.ACCT =  R.NEW(FT.CREDIT.ACCT.NO)
    GOSUB GET.ACCOUNT.NAME
    TRXN.REC<CAB.FTLOG.CR.CATEGORY> =  Y.CATEGORY
    TRXN.REC<CAB.FTLOG.CREDIT.ACCT.NAME> = Y.ACCT.ID.DES

    TRXN.REC<CAB.FTLOG.AMOUNT.CREDITED> =  R.NEW(FT.AMOUNT.CREDITED)
    TRXN.REC<CAB.FTLOG.CREDIT.VALUE.DATE> =  R.NEW(FT.CREDIT.VALUE.DATE)
    TRXN.REC<CAB.FTLOG.CREDIT.THEIR.REF> =  R.NEW(FT.CREDIT.THEIR.REF)
    TRXN.REC<CAB.FTLOG.COMMISSION.TYPE> =  R.NEW(FT.COMMISSION.TYPE)

    GOSUB GET.COMMISSION.DESCRIPTION
    TRXN.REC<CAB.FTLOG.COMMISSION.DES> =  R.FT.COMMISSION.TYPE<FT4.DESCRIPTION>


    TRXN.REC<CAB.FTLOG.COMMISSION.AMT> = R.NEW(FT.COMMISSION.AMT)
    TRXN.REC<CAB.FTLOG.PAY.DET.ID> = R.NEW(FT.LOCAL.REF)<1,PAY.DET.ID.POS>

    CALL F.WRITE(FN.CAB.FT.TXNS.LOG,ID.NEW,TRXN.REC)

    RETURN

GET.TRANSACTION.DESC:

    IF R.NEW(FT.TRANSACTION.TYPE) THEN
        FN.FT.TXN.TYPE.CONDITION = 'F.FT.TXN.TYPE.CONDITION'
        F.FT.TXN.TYPE.CONDITION = ''
        CALL OPF(FN.FT.TXN.TYPE.CONDITION,F.FT.TXN.TYPE.CONDITION)
        R.FT.TXN.TYPE.CONDITION = ''
        E.FT.TXN.TYPE.CONDITION = ''
        FT.TXN.TYPE.CONDITION.ID = ''
        FT.TXN.TYPE.CONDITION.ID = R.NEW(FT.TRANSACTION.TYPE)
        CALL F.READ(FN.FT.TXN.TYPE.CONDITION,FT.TXN.TYPE.CONDITION.ID,R.FT.TXN.TYPE.CONDITION,F.FT.TXN.TYPE.CONDITION,E.FT.TXN.TYPE.CONDITION)
    END

    RETURN

GET.ACCOUNT.NAME:

    IF YR.ACCT THEN
        FN.ACCOUNT = 'F.ACCOUNT'
        F.ACCOUNT = ''
        CALL OPF(FN.ACCOUNT,F.ACCOUNT)

        FN.ACCOUNT.HIS = 'F.ACCOUNT$HIS'
        F.ACCOUNT.HIS = ''
        CALL OPF(FN.ACCOUNT.HIS,F.ACCOUNT.HIS)

        R.ACCOUNT = ''
        ACCT.ERR = ''

        CALL F.READ(FN.ACCOUNT,YR.ACCT,R.ACCOUNT,F.ACCOUNT,ACCT.ERR)
        IF R.ACCOUNT EQ '' THEN
            ACCT.ERR.HIS = ''
            CALL EB.READ.HISTORY.REC(F.ACCOUNT.HIS,YR.ACCT,R.ACCOUNT,ACCT.ERR.HIS)
        END
        Y.ACCT.ID.DES = R.ACCOUNT<AC.SHORT.TITLE>
        Y.CATEGORY = R.ACCOUNT<AC.CATEGORY>
    END

    RETURN

GET.COMMISSION.DESCRIPTION:
    IF R.NEW(FT.TRANSACTION.TYPE) THEN
        FN.FT.COMMISSION.TYPE = 'F.FT.COMMISSION.TYPE'
        F.FT.COMMISSION.TYPE = ''
        CALL OPF(FN.FT.COMMISSION.TYPE,F.FT.COMMISSION.TYPE)
        R.FT.COMMISSION.TYPE = ''
        E.FT.COMMISSION.TYPE = ''
        FT.COMMISSION.TYPE.ID = ''
        FT.COMMISSION.TYPE.ID = R.NEW(FT.TRANSACTION.TYPE)
        CALL F.READ(FN.FT.COMMISSION.TYPE,FT.COMMISSION.TYPE.ID,R.FT.COMMISSION.TYPE,F.FT.COMMISSION.TYPE,E.FT.COMMISSION.TYPE)
    END

    RETURN

END
