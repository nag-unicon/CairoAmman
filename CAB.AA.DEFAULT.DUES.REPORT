    SUBROUTINE CAB.AA.DEFAULT.DUES.REPORT(BILL.ID)
*************************************************************************************
*************************************************************************************
*** CAB Jordan Defaulter report
*** For Salary system
***
***
*************************************************************************************
*************************************************************************************

    $INCLUDE T24.BP I_COMMON
    $INCLUDE T24.BP I_EQUATE
    $INCLUDE T24.BP I_F.AA.ARRANGEMENT
    $INCLUDE T24.BP I_F.AA.BILL.DETAILS
    $INCLUDE T24.BP I_F.AA.INTEREST.ACCRUALS
    $INCLUDE T24.BP I_F.AA.ACCOUNT.DETAILS
    $INCLUDE T24.BP I_F.AA.ACCOUNT
    $INCLUDE T24.BP I_F.AC.CASH.POOL
    $INCLUDE T24.BP I_F.ACCOUNT
    $INCLUDE T24.BP I_F.ALTERNATE.ACCOUNT
    $INCLUDE AN.BP I_CAB.SAL.COMMON
    $INCLUDE AN.BP I_F.CAB.SALARY.REPORT.LIST

    GOSUB INIT
    GOSUB PROCESS

    RETURN

******
INIT:
******

    UPDATE.TYPE=''
    RET.ACCT.ERROR=''
    EFFECTIVE.DATE = TODAY
    PROCESS.TYPE = 'INITIALISE'
    WRITE.FLAG = '1'

    CALL F.READ(FN.AA.BILL.DETAILS,BILL.ID,BILL.DETAILS,F.AA.BILL.DETAILS,ERR.BILL.DT)
    ARR.ID = BILL.DETAILS<AA.BD.ARRANGEMENT.ID>

    CALL AA.PROCESS.ACCOUNT.DETAILS(ARR.ID, PROCESS.TYPE, UPDATE.TYPE,R.AA.ACC.DET, RET.ACCT.ERROR)
    CALL AA.GET.ARRANGEMENT(ARR.ID,R.ARR,ERR.ARR)
    CALL AA.GET.PROPERTY.RECORD('', ARR.ID, 'ACCOUNT', EFFECTIVE.DATE, 'ACCOUNT', '', R.ACCOUNT.RECORD, R.ERR)

*** Get Loan Static data

    LOAN.ACC = R.ARR<AA.ARR.LINKED.APPL.ID,1>
    ARR.ACC.NUMBER = LOAN.ACC
    ARR.FWD.CCY = R.ARR<AA.ARR.CURRENCY>
    ARR.COMPANY.ID= R.ARR<AA.ARR.CO.CODE>

    GOSUB DETERMINE.SETTLE.ACCOUNT

    RETURN

*************************
DETERMINE.SETTLE.ACCOUNT:
*************************

*** 1st position Settlement Account took it from Arrangement Level
    SETT.ACC = ''
    SETT.ACC = R.ACCOUNT.RECORD<AA.AC.LOCAL.REF,SETT.POS,1>

*** 2nd Level AC.CASH.POOL
    R.SETTLEMENT.ACCOUNT =''

    CALL F.READ(FN.SETT.ACC,SETT.ACC,R.SETTLEMENT.ACCOUNT,F.SETT.ACC,ERR.SETT.ACC)

    IF R.SETTLEMENT.ACCOUNT EQ '' THEN
        CALL F.READ(FN.AC.CASH.POOL,LOAN.ACC,R.AC.CASH.POOL,F.AC.CASH.POOL,REC.ERR)
        SETT.ACC = R.AC.CASH.POOL<AC.CP.LINK.ACCT,1>
        CALL F.READ(FN.SETT.ACC,SETT.ACC,R.SETTLEMENT.ACCOUNT,F.SETT.ACC,ERR.SETT.ACC)
    END

    SETTLEMENT.CCY = R.SETTLEMENT.ACCOUNT<AC.CURRENCY>
    SETTLEMENT.COMPANY = R.SETTLEMENT.ACCOUNT<AC.CO.CODE>

    RETURN

********
PROCESS:
********

    GOSUB PROCESS.DUE

    RETURN

************
PROCESS.DUE:
************


*** Normal Loans

    PROPERTY =BILL.DETAILS<AA.BD.PROPERTY>
    BILL.AMOUNT = BILL.DETAILS<AA.BD.OS.TOTAL.AMOUNT>
    DUE.AMOUNT = BILL.DETAILS<AA.BD.OS.TOTAL.AMOUNT>
    REPAID.AMOUNT =BILL.DETAILS<AA.BD.OR.TOTAL.AMOUNT> - BILL.DETAILS<AA.BD.OS.TOTAL.AMOUNT>
    BILL.DATE = BILL.DETAILS<AA.BD.PAYMENT.DATE,1>
    PAYMENT.TYPE = BILL.DETAILS<AA.BD.PAYMENT.TYPE>
    ARRANGEMENT.ID = BILL.DETAILS<AA.BD.ARRANGEMENT.ID>

    GOSUB CHECK.SPECIAL.PROCESS

    R.PEN.INT<AAA.SETT.ACC> = SETT.ACC
    R.PEN.INT<AAA.SETT.COM> = SETTLEMENT.COMPANY
    R.PEN.INT<AAA.SETT.CCY> = SETTLEMENT.CCY
    R.PEN.INT<AAA.BILL.AMT> = BILL.AMOUNT
    R.PEN.INT<AAA.DUE.AMT> = DUE.AMOUNT
    R.PEN.INT<AAA.RPY.AMT> = REPAID.AMOUNT
    R.PEN.INT<AAA.BILL.DATE> = BILL.DATE
    R.PEN.INT<AAA.PAY.TYPE> = PAYMENT.TYPE
    R.PEN.INT<AAA.LOAN.ACC> = ARR.ACC.NUMBER
    R.PEN.INT<AAA.LOAN.CCY> = ARR.FWD.CCY
    R.PEN.INT<AAA.LOAN.COM> = ARR.COMPANY.ID
    R.PEN.INT<AAA.FT.TXN.TYPE> = "ACRP"

    IF WRITE.FLAG AND BILL.AMOUNT GT '0' THEN
        CALL F.READ(FN.SETT.ACC,SETT.ACC,R.SETT.ACC,F.SETT.ACC,ERR.ACC)
        R.PEN.INT<AAA.SETT.ACC> = R.SETT.ACC<AC.ALT.ACCT.ID,1>
        IF R.PEN.INT<AAA.SETT.ACC> NE '' THEN
            CALL F.WRITE(FN.CAB.SAL,BILL.ID,R.PEN.INT)
        END
    END

    RETURN
**********************
CHECK.SPECIAL.PROCESS:
**********************
*** Housing sub Loan special check
    SUBS.POS =''
    PROP.POS =''
    LOCATE 'SUBSIDY' IN PAYMENT.TYPE<1,1> SETTING SUBS.POS THEN
        LOCATE 'HOUSESUBLOW' IN PROPERTY<1,1> SETTING PROP.POS THEN
            BILL.AMOUNT = BILL.DETAILS<AA.BD.OS.TOTAL.AMOUNT>
            PROP.AMT = BILL.DETAILS<AA.BD.OS.PROP.AMOUNT,PROP.POS>
            BILL.AMOUNT = BILL.AMOUNT - PROP.AMT
            IF BILL.AMOUNT EQ '0' THEN
                WRITE.FLAG = '0'
            END
            DUE.AMOUNT = BILL.AMOUNT
            REPAID.AMOUNT = BILL.DETAILS<AA.BD.OR.TOTAL.AMOUNT> - BILL.DETAILS<AA.BD.PAYMENT.AMOUNT,SUBS.POS>
            BILL.DATE = BILL.DETAILS<AA.BD.PAYMENT.DATE,1>
        END
    END

    RETURN
