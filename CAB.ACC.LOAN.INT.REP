    SUBROUTINE CAB.ACC.LOAN.INT.REP(RET.ARR)

* Date - 26-AUG-2014
* Desc - This routine is to generate the Customer wise report for all Loans.
* Attached Enquiry -
* Author - Bala Gnana Pandian
* Input - Customer Number mandatory, From date & To date optional
* Output - Return array will containg the list of loans / Separate bills for each loan / Property

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_ENQUIRY.COMMON
    $INSERT I_F.AA.ARRANGEMENT
    $INSERT I_F.AA.BILL.DETAILS
    $INSERT I_F.AA.INTEREST.ACCRUALS
    $INSERT I_F.AA.ACTIVITY.BALANCES

    GOSUB INIT
    GOSUB GET.BASIC.DATA

    IF CUSTOMER.NO THEN
        GOSUB SEL.CUS.ACCT
    END ELSE
        GOSUB GET.INDIVIDUAL.CUST
    END

    GOSUB GET.PROP.LIST

    RETURN

INIT:

    FN.AA.ARRANGEMENT = 'F.AA.ARRANGEMENT'
    F.AA.ARRANGEMENT  = ''
    CALL OPF(FN.AA.ARRANGEMENT,F.AA.ARRANGEMENT)

    FN.AA.BILL.DETAILS = 'F.AA.BILL.DETAILS'
    F.AA.BILL.DETAILS = ''
    CALL OPF(FN.AA.BILL.DETAILS,F.AA.BILL.DETAILS)

    FN.AA.INTEREST.ACCRUALS ='F.AA.INTEREST.ACCRUALS'
    F.AA.INTEREST.ACCRUALS = ''
    CALL OPF(FN.AA.INTEREST.ACCRUALS,F.AA.INTEREST.ACCRUALS)

    FN.AA.ACTIVITY.BALANCES = "F.AA.ACTIVITY.BALANCES"
    F.AA.ACTIVITY.BALANCES = ""
    CALL OPF(FN.AA.ACTIVITY.BALANCES, F.AA.ACTIVITY.BALANCES)

    EFF.DATE = TODAY

    RETURN

GET.BASIC.DATA:

    LOCATE 'CUSTOMER.NO' IN ENQ.SELECTION<2,1> SETTING CUS.POS THEN
        CUSTOMER.NO = ENQ.SELECTION<4,CUS.POS>
    END

    LOCATE 'FROM.DATE' IN ENQ.SELECTION<2,1> SETTING FRM.POS THEN
        FROM.DATE = ENQ.SELECTION<4,FRM.POS>
    END ELSE
        FROM.DATE = TODAY
        YEAR = FROM.DATE[1,4]
        MNTH = FROM.DATE[5,2]
        IF MNTH MATCHES '01':VM:'02':VM:'03' THEN
            YEAR = YEAR - 1
        END
        FROM.DATE = YEAR:"0401"
    END

    LOCATE 'TO.DATE' IN ENQ.SELECTION<2,1> SETTING TO.POS THEN
        TO.DATE = ENQ.SELECTION<4,TO.POS>
    END ELSE
        TO.DATE = TODAY
    END

    LOCATE 'BRANCH.CODE' IN ENQ.SELECTION<2,1> SETTING BRN.POS THEN
        BR.CODE = ENQ.SELECTION<4,BRN.POS>
    END

    LOAN.INT = "EALLLOANINT"
    CALL AA.GET.ENQUIRY.BALANCE.TYPE(LOAN.INT)
    LOAN.CHG= "EALLLOANCRG"
    CALL AA.GET.ENQUIRY.BALANCE.TYPE(LOAN.CHG)

    RETURN

SEL.CUS.ACCT:

    SEL.LIST = ''
    SEL.CMD = 'SELECT ': FN.AA.ARRANGEMENT : ' WITH PRODUCT.LINE EQ LENDING AND CUSTOMER EQ ':CUSTOMER.NO :' BY ARRANGEMENT.ID'
    CALL EB.READLIST( SEL.CMD,SEL.LIST,'',NO.OF.REC,SEL.ERR)

    RETURN

GET.INDIVIDUAL.CUST:

    SEL.LIST = ''
    SEL.CMD = 'SELECT ': FN.AA.ARRANGEMENT : ' WITH PRODUCT.LINE EQ LENDING BY CUSTOMER'
    CALL EB.READLIST( SEL.CMD,SEL.LIST,'',NO.OF.REC,SEL.ERR)

    RETURN

GET.PROP.LIST:

    LOOP
        REMOVE ARR.ID FROM SEL.LIST SETTING ARR.POS
    WHILE ARR.ID : ARR.POS

        ARR.FLAG = 1 ; ACCT.ID = '';ARR.REC= '';PRODUCT.ID = '';PROPERTY.LIST = ''
        CALL AA.GET.ARRANGEMENT.PRODUCT(ARR.ID,EFF.DATE,ARR.REC,PRODUCT.ID,PROPERTY.LIST)
        ACCT.ID = ARR.REC<AA.ARR.LINKED.APPL.ID>
        CUSTOMER.NO = ARR.REC<AA.ARR.CUSTOMER>

        GOSUB GET.BILL.DETAILS

        IF BR.CODE EQ '' OR ARR.REC<AA.ARR.CO.CODE> EQ BR.CODE THEN
            GOSUB PROCESS
        END

    REPEAT

    RETURN


GET.BILL.DETAILS:

    BILL.TYPE = '';
    BILL.STATUS = ''
    PAYMENT.METHOD = ''
    BILL.REFERENCES = ''
    RET.ERROR = ''
    PROCESS.TYPE = "INITIALISE"

    CALL AA.PROCESS.ACCOUNT.DETAILS(ARR.ID, PROCESS.TYPE, "", R.AA.ACCOUNT.DETAILS, RET.ERROR)

    CALL AA.GET.BILL(ARR.ID, "", "", "", BILL.TYPE, PAYMENT.METHOD, BILL.STATUS, "", "", "", "", BILL.REFERENCES, RET.ERROR)

    RETURN

BILL.PROCESS:

    BILL.PROPERTY = BILL.REC<AA.BD.PROPERTY>
    BILL.PROP.CNT = DCOUNT(BILL.PROPERTY,VM)
    PAYMENT.DATE = BILL.REC<AA.BD.PAYMENT.DATE>
    PROCESS.FLAG = 0

    FOR I = 1 TO BILL.PROP.CNT

        BILL.PROP = BILL.PROPERTY<1,I>

        IF BILL.REC<AA.BD.BILL.TYPE, 1> EQ "PAYOFF" AND BILL.REC<AA.BD.PAYMENT.METHOD,1> EQ "INFO" THEN

            IF NOT(PROCESS.FLAG) THEN

                R.AA.ACTIVITY.BALANCES = ""
                ERR.AA.ACTIVITY.BALANCES = ""
                CALL F.READ(FN.AA.ACTIVITY.BALANCES, ARR.ID, R.AA.ACTIVITY.BALANCES, F.AA.ACTIVITY.BALANCES, ERR.AA.ACTIVITY.BALANCES)

                PROCESS.FLAG = 1
                PROP.OS.AMT = ''
                PROP.OR.AMT = ''

                LOCATE "LENDING-APPLYPAYMENT-PR.PAYOFF" IN R.AA.ACTIVITY.BALANCES<AA.ACT.BAL.ACTIVITY, 1> SETTING ACT.POS THEN
                    DTE.OF.SET  = R.AA.ACTIVITY.BALANCES<AA.ACT.BAL.ACTIVITY.DATE, ACT.POS>
                    GOSUB PROCESS.PAYOFF.BILL.DETAILS
                END

                LOCATE "LENDING-APPLYPAYMENT-PR.PRINCIPAL.DECREASE" IN R.AA.ACTIVITY.BALANCES<AA.ACT.BAL.ACTIVITY, 1> SETTING ACT.POS THEN
                    DTE.OF.SET  = R.AA.ACTIVITY.BALANCES<AA.ACT.BAL.ACTIVITY.DATE, ACT.POS>
                    GOSUB PROCESS.PAYOFF.BILL.DETAILS
                END

                IF PROP.OR.AMT GT 0 THEN
                    PAYMENT.DATE = R.AA.ACTIVITY.BALANCES<AA.ACT.BAL.ACTIVITY.DATE, ACT.POS>
                    GOSUB ADD.INT.AMT
                END
            END

        END ELSE

            IF BILL.PROP MATCHES LOAN.INT THEN
                GOSUB GET.OS.AMT
                GOSUB ADD.INT.AMT
            END

            IF BILL.PROP MATCHES LOAN.CHG THEN
                GOSUB GET.OS.AMT
                GOSUB ADD.CHG.AMT
            END
        END

    NEXT I

    RETURN


PROCESS.PAYOFF.BILL.DETAILS:

    PROPERTY.LIST = R.AA.ACTIVITY.BALANCES<AA.ACT.BAL.PROPERTY, ACT.POS>
    PROPERTY.AMTS = R.AA.ACTIVITY.BALANCES<AA.ACT.BAL.PROPERTY.AMT, ACT.POS>

    CHANGE SM TO VM IN PROPERTY.LIST
    CHANGE SM TO VM IN PROPERTY.AMTS

    TOTAL.PROPERTY = DCOUNT(PROPERTY.LIST, VM)
    REC.CNT = 1

    LOOP
    WHILE REC.CNT LE TOTAL.PROPERTY

        IF PROPERTY.LIST<1, REC.CNT> EQ "PRINCIPALINT.ACCPRINCIPALINT" OR PROPERTY.LIST<1, REC.CNT> EQ "PENALTYINT.ACCPENALTYNT" THEN
            PROP.OR.AMT + = PROPERTY.AMTS<1,REC.CNT>
        END

        REC.CNT + =1
    REPEAT


    RETURN

GET.OS.AMT:

    PROP.OR.AMT = ''; DTE.OF.SET = '';
    PROP.OS.AMT = '';

    PROP.OR.AMT = FMT(BILL.REC<AA.BD.OR.PROP.AMOUNT,I>,"R3#19")
    PROP.OS.AMT = FMT(BILL.REC<AA.BD.OS.PROP.AMOUNT,I>,"R3#19")
    IF PROP.OS.AMT EQ "0" OR PROP.OS.AMT EQ '' THEN
        DTE.OF.SET = BILL.REC<AA.BD.BILL.ST.CHG.DT,1>
    END

    RETURN


ADD.INT.AMT:

    IF ARR.FLAG THEN

        RET.ARR<-1> = CUSTOMER.NO :"*" :ARR.ID :"*" : ACCT.ID : "*" : ARR.REC<AA.ARR.CO.CODE> : "*" : PRODUCT.ID : "*" : PROP.OR.AMT
        RET.ARR := "*":"*":BILL.ID :"*":PROP.OS.AMT:"*":PAYMENT.DATE:"*":DTE.OF.SET:"*":FROM.DATE:"*":TO.DATE
        ARR.FLAG = 0
    END ELSE
        RET.ARR<-1> = "*":"*":"*":"*":"*":PROP.OR.AMT:"*":"*":BILL.ID:"*":PROP.OS.AMT:"*":BILL.REC<AA.BD.PAYMENT.DATE>:"*":DTE.OF.SET:"*":FROM.DATE:"*":TO.DATE
    END
    RETURN


ADD.CHG.AMT:

    IF ARR.FLAG THEN
        RET.ARR<-1> = CUSTOMER.NO :"*" :ARR.ID :"*" : ACCT.ID : "*" : ARR.REC<AA.ARR.CO.CODE> : "*" : PRODUCT.ID : "*" : "*":PROP.OR.AMT
        RET.ARR := "*":BILL.ID:"*":PROP.OS.AMT:"*":BILL.REC<AA.BD.PAYMENT.DATE>:"*":DTE.OF.SET:"*":FROM.DATE:"*":TO.DATE
        ARR.FLAG = 0
    END ELSE
        RET.ARR<-1> = "*":"*":"*":"*":"*":"*":PROP.OR.AMT:"*" : BILL.ID:"*":PROP.OS.AMT:"*":BILL.REC<AA.BD.PAYMENT.DATE>:"*":DTE.OF.SET:"*":FROM.DATE:"*":TO.DATE
    END
    RETURN


PROCESS:

    LOOP
        REMOVE BILL.ID FROM BILL.REFERENCES SETTING BILL.POS
    WHILE BILL.ID : BILL.POS
        BILL.REC = ''
        CALL AA.GET.BILL.DETAILS(ARR.ID,BILL.ID,BILL.REC,RET.ERR)
        IF BILL.REC<AA.BD.PAYMENT.TYPE,1> NE "PAYOFF" AND BILL.REC<AA.BD.PAYMENT.DATE> GE FROM.DATE AND BILL.REC<AA.BD.PAYMENT.DATE> LE TO.DATE THEN
            GOSUB BILL.PROCESS
        END
    REPEAT

    RETURN

END
