    SUBROUTINE CAB.AA.PENDING.CASHPOOL.REVERSE(ARRANGEMENT.ID)

* Desc   - Multithread routine developed to reverse the cashpool automatically during COB.
* Author - BalaGnanaPandian
* ITSS Development

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_GTS.COMMON
    $INSERT I_BATCH.FILES
    $INSERT I_CAB.AA.PENDING.CASHPOOL.REVERSE.COMMON
    $INSERT I_F.AA.ARRANGEMENT
    $INSERT I_F.AC.CASH.POOL

    GOSUB GET.BASIC.DATA
    RETURN

GET.BASIC.DATA:
    EFF.DATE = TODAY
    theResponse = ""
    txnCommitted = ""
    R.ARR.LIST = ''

    CALL AA.GET.ARRANGEMENT.PRODUCT(ARRANGEMENT.ID,EFF.DATE,R.AA.ARRANGEMENT,PRODUCT.ID,PROPERTY.LIST)

    ACC.NO = R.AA.ARRANGEMENT<AA.ARR.LINKED.APPL.ID>
    CALL F.READ(FN.AC.CASHPOOL,ACC.NO,R.CASH.POOL,F.AC.CASHPOOL,E.CASHPOOL)
    C.CO.CODE = R.CASH.POOL<AC.CP.CO.CODE>
    CALL LOAD.COMPANY(C.CO.CODE)

    IF E.CASHPOOL THEN
        theResponse = E.CASHPOOL
        txnCommitted = '0'
        GOSUB UPDATE.CONCATE.FILE
    END ELSE
        GOSUB FORM.OFS.DETAILS
        GOSUB UPDATE.CONCATE.FILE
    END
    RETURN

FORM.OFS.DETAILS:
    AC.CASH.REQUEST = ''

    APP.NAME = "AC.CASH.POOL"
    OFS.RECORD = ''
    OFSVERSION = APP.NAME:","
    NO.OF.AUTH = "0"
    ARR.ACTIVITY.ID =''
    FUNCT ='R'
    options = ''
    options<1> = "AA.COB"
* options<4> = "HLD"

    CALL OFS.BUILD.RECORD(APP.NAME, FUNCT, "PROCESS", OFSVERSION, "", NO.OF.AUTH, ACC.NO, AC.CASH.REQUEST, OFS.RECORD)
    OFS.REQ = OFS.RECORD


    CALL OFS.CALL.BULK.MANAGER(options,OFS.REQ,theResponse,txnCommitted)

    RETURN

UPDATE.CONCATE.FILE:

    IF txnCommitted EQ '1' THEN
        REC.ID = "REVE.CASH.POOL":"-":EFF.DATE : "-" : SESSION.NO
    END ELSE
        REC.ID = "REVE.CASH.POOL-ERR":"-":EFF.DATE : "-" : SESSION.NO
    END
    REC.DATA = ARRANGEMENT.ID :"*":ACC.NO:"*":PRODUCT.ID :"*":C.CO.CODE:"*":EFF.DATE:"*":txnCommitted:"*":theResponse
    CALL F.READ(FN.AA.AC.PENDING,REC.ID,R.ARR.LIST,F.AA.AC.PENDING,ERR.PENDING)

    R.ARR.LIST <-1> = REC.DATA
    CALL F.WRITE(FN.AA.AC.PENDING,REC.ID,R.ARR.LIST)
    RETURN
END
