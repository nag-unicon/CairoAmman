    SUBROUTINE CAB.ATM.LOANS.IN.MSG.RTN(IN.MSG)

    $INSERT T24.BP I_COMMON
    $INSERT T24.BP I_EQUATE
    $INSERT T24.BP I_GTS.COMMON

    $INCLUDE CAB.BP I_F.CAB.ATM.LOANS.PARAM
    $INCLUDE CAB.BP I_F.CAB.ATM.LOANS.DETAILS
    $INCLUDE CAB.BP I_CAB.ATM.LOANS.COMMON

    GOSUB INIT
    GOSUB OPEN.FILES
    GOSUB SET.FIELD.POSITION
    GOSUB PROCESS

    RETURN

INIT:
*---*
    OFS.USER = ''
    OFS.PASSWD = ''
    DUP.TXN = 0

    FN.CAB.ATM.LOANS.PARAM = 'F.CAB.ATM.LOANS.PARAM'
    F.CAB.ATM.LOANS.PARAM = ''
    CAB.ATM.LOANS.PARAM.ID = 'SYSTEM'
    R.CAB.ATM.LOANS.PARAM = ''
    CAB.ATM.LOANS.PARAM.ERR = ''

    FN.CAB.ATM.LOANS.DETAILS = 'F.CAB.ATM.LOANS.DETAILS'
    F.CAB.ATM.LOANS.DETAILS = ''
    R.CAB.ATM.LOANS.DETAILS = ''
    CAB.ATM.LOANS.DETAILS.ERR = ''

    IN.MSG.FIRST.PART = ''
    IN.MSG.TXN.ID = ''
    IN.MSG.SECOND.PART = ''
    IN.MSG.DATA.FIELDS = ''

    RETURN

OPEN.FILES:
*---------*
    CALL OPF(FN.CAB.ATM.LOANS.PARAM,F.CAB.ATM.LOANS.PARAM)
    CALL OPF(FN.CAB.ATM.LOANS.DETAILS,F.CAB.ATM.LOANS.DETAILS)

    RETURN

SET.FIELD.POSITION:
*-----------------*

    BEGIN CASE
    CASE OFS.APPLN EQ "LIMIT"
        ATM.LOANS.TXN.ID = CAB.ATM.L.DET.LIMIT.TXN.ID
        ATM.LOANS.TXN.STATUS = CAB.ATM.L.DET.LIM.TXN.STAT
        ATM.LOANS.T24.RESP = CAB.ATM.L.DET.LIM.T24.RESP
    CASE OFS.APPLN EQ "AA.ARRANGEMENT.ACTIVITY"
        ATM.LOANS.TXN.ID = CAB.ATM.L.DET.AAA.TXN.ID
        ATM.LOANS.TXN.STATUS = CAB.ATM.L.DET.AAA.TXN.STAT
        ATM.LOANS.T24.RESP = CAB.ATM.L.DET.AAA.INP.RESP
    CASE OFS.APPLN EQ "FUNDS.TRANSFER"
        ATM.LOANS.TXN.ID = CAB.ATM.L.DET.FT.TXN.ID
        ATM.LOANS.TXN.STATUS = CAB.ATM.L.DET.FT.TXN.STAT
        ATM.LOANS.T24.RESP = CAB.ATM.L.DET.FT.T24.RESP
    CASE OFS.APPLN EQ "AC.CASH.POOL"
        ATM.LOANS.TXN.ID = CAB.ATM.L.DET.ACCP.TXN.ID
        ATM.LOANS.TXN.STATUS = CAB.ATM.L.DET.ACCP.TXN.STAT
        ATM.LOANS.T24.RESP = CAB.ATM.L.DET.ACCP.T24.RESP
    END CASE

    RETURN

PROCESS:
*------*
    CALL CACHE.READ(FN.CAB.ATM.LOANS.PARAM,CAB.ATM.LOANS.PARAM.ID,R.CAB.ATM.LOANS.PARAM,CAB.ATM.LOANS.PARAM.ERR)

    IN.MSG.FIRST.PART = FIELDS(IN.MSG,',',1,2)
    IN.MSG.TXN.ID = FIELD(IN.MSG,',',4)
    IN.MSG.SECOND.PART = FIELDS(IN.MSG,',',5,99999)
    IN.MSG.DATA.FIELDS = IN.MSG.SECOND.PART
    OFS.VERSION = FIELD(IN.MSG.FIRST.PART,"/",1)
    OFS.APPLN = FIELD(OFS.VERSION,',',1)
    OFS.FUNCTION = FIELD(IN.MSG.FIRST.PART,"/",2)
    PROCESS.TYPE = FIELD(IN.MSG.FIRST.PART,"/",3)

    IF R.CAB.ATM.LOANS.PARAM NE '' THEN
        OFS.USER = R.CAB.ATM.LOANS.PARAM<CAB.ATM.LOAN.OFS.USER>
        OFS.PASSWD = R.CAB.ATM.LOANS.PARAM<CAB.ATM.LOAN.OFS.PASSWORD>
    END

    GOSUB EXTRACT.FIELDS
    GOSUB CHECK.DUPLICATE

    IF DUP.TXN THEN
        IN.MSG = OFS.VERSION:"/":OFS.FUNCTION:"/":PROCESS.TYPE:",":OFS.USER:"/":OFS.PASSWD:",":IN.MSG.TXN.ID:",":IN.MSG.SECOND.PART
    END ELSE
        IN.MSG = OFS.VERSION:"/":OFS.FUNCTION:"/":PROCESS.TYPE:"/":YR.GTS.MODE:",":OFS.USER:"/":OFS.PASSWD:",":IN.MSG.TXN.ID:",":IN.MSG.SECOND.PART
    END

    RETURN

EXTRACT.FIELDS:
*-------------*
    CHANGE ',' TO FM IN IN.MSG.DATA.FIELDS

    IN.MSG.FIELD.NAMES = ''
    IN.MSG.FIELD.VALUES = ''
    IN.MSG.SECOND.PART = ''

    LOOP
        REMOVE IN.MSG.DATA.FIELD FROM IN.MSG.DATA.FIELDS SETTING IN.MSG.DATA.FIELD.POS
    WHILE IN.MSG.DATA.FIELD:IN.MSG.DATA.FIELD.POS
        IN.MSG.FIELD.NAME = FIELD(IN.MSG.DATA.FIELD,':',1)
        IN.MSG.FIELD.VALUE = FIELD(IN.MSG.DATA.FIELD,'=',2)
        IN.MSG.FIELD.NAMES<-1> = IN.MSG.FIELD.NAME
        IN.MSG.FIELD.VALUES<-1> = IN.MSG.FIELD.VALUE
        IF IN.MSG.FIELD.NAME NE 'CBSTXNREF' THEN
            IN.MSG.SECOND.PART := IN.MSG.DATA.FIELD:","
        END
    REPEAT

    RETURN

CHECK.DUPLICATE:
*--------------*

    LOCATE 'CBSTXNREF' IN IN.MSG.FIELD.NAMES SETTING IN.MSG.FIELD.NAME.POS  THEN
        YR.TXN.REF = IN.MSG.FIELD.VALUES<IN.MSG.FIELD.NAME.POS>
    END ELSE
        YR.TXN.REF = ''
    END

    LOCATE 'TXN.TYPE' IN IN.MSG.FIELD.NAMES SETTING TXN.FIELD.NAME.POS THEN
        YR.TXN.TYPE = IN.MSG.FIELD.VALUES<TXN.FIELD.NAME.POS>
    END ELSE
        YR.TXN.TYPE = ''
    END

    CALL F.READ(FN.CAB.ATM.LOANS.DETAILS,YR.TXN.REF,R.CAB.ATM.LOANS.DETAILS,F.CAB.ATM.LOANS.DETAILS,CAB.ATM.LOANS.DETAILS.ERR)

    IF R.CAB.ATM.LOANS.DETAILS NE '' THEN
        DUP.TXN = 1
        OFS.FUNCTION = 'S'
        IN.MSG.TXN.ID = R.CAB.ATM.LOANS.DETAILS<ATM.LOANS.TXN.ID>
    END


    BEGIN CASE
    CASE YR.TXN.TYPE EQ '2'
        YR.GTS.MODE = '0'

    CASE YR.TXN.TYPE EQ '1'
        YR.GTS.MODE = '2'

    CASE YR.TXN.TYPE EQ '4'
        YR.GTS.MODE = '0'
        OFS.FUNCTION = 'R'

    END CASE

    RETURN
END
