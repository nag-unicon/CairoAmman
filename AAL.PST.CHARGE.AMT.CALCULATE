    SUBROUTINE AAL.PST.CHARGE.AMT.CALCULATE
* ----------------------------------------------------------------------------------------------
* Company Name     : Cairo Amman Bank
* Developed By     : Muthukaruppan S
* Development Id   : ITSS
* Date             : 20th Aug 2017
*----------------------------------------------------------------------------------------------
* Subroutine Type  : Pre routine
* Attached to      :     Activity Class  : LENDING-NEW-ARRANGEMENT
*                        Property Class  : PAYMENT.SCHEDULE
*                        Action          : UPDATE
* Attached As      : Activity API - Pre Routine
* ----------------------------------------------------------------------------------------------
* Primary Purpose  : This Routine is used to Calculate the Discounted Interest (Charge) amount and commission amount by bills
* ----------------------------------------------------------------------------------------------
* ARGS -(Input)    : NA
* ARGS -(Output)   : NA
* ----------------------------------------------------------------------------------------------
* Modification History:
* ---------------------
* Modification Ref :
* Modification Date:
* Modified by      :
* Modifi. Descript :
* ----------------------------------------------------------------------------------------------
*** <region name= Inserts>
***
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.AA.PAYMENT.SCHEDULE
    $INSERT I_F.AA.CHARGE
    $INSERT I_AA.LOCAL.COMMON
    $INSERT I_F.AA.ARRANGEMENT.ACTIVITY

*** </region>
*** <region name= Main Body>

    IF c_aalocCurrActivity EQ 'LENDING-NEW-ARRANGEMENT' AND c_aalocActivityStatus["-",1,1] EQ 'UNAUTH' THEN

        GOSUB GET.LOCAL.REF.FIELDS
        GOSUB GET.REQUIRED.INFO
        GOSUB VALIDATE.INTEREST.AND.CHARGE.AMT
    END

    RETURN

*** </region>

*** <region name= GET.LOCAL.REF.FIELDS>

GET.LOCAL.REF.FIELDS:

    APPLN.NAME = ''
    FIELD.NAME = ''
    LOCAL.POS = ''

    APPLN.NAME = 'AA.PRD.DES.PAYMENT.SCHEDULE'
    FIELD.NAME = 'TOTAL.INTEREST' :VM: 'TOTAL.COMM'
    CALL MULTI.GET.LOC.REF(APPLN.NAME,FIELD.NAME,LOCAL.POS)

    TOTAL.INTEREST.POS = LOCAL.POS<1,1>
    TOTAL.COMM.POS     = LOCAL.POS<1,2>

    RETURN

*** </region>

*** <region name= GET.REQUIRED.INFO>

GET.REQUIRED.INFO:

    ARR.ID             = c_aalocArrId
    ARR.EFFECTIVE.DATE = c_aalocActivityEffDate
    TOTAL.PAYMENT.TYPES= DCOUNT(R.NEW(AA.PS.PAYMENT.TYPE), VM)
    effectiveDate      = c_aalocActivityEffDate

    RETURN

*** </region>

*** <region name= VALIDATE.INTEREST.AND.CHARGE.AMT>

VALIDATE.INTEREST.AND.CHARGE.AMT:

    property = 'DISCOUNTINT'
    GOSUB GET.ARRANGEMENT.CONDITIONS
    DISCOUNTINT.AMOUNT = R.AA.CHARGE<AA.CHG.FIXED.AMOUNT>

    property = 'ANNUALCOMM'
    GOSUB GET.ARRANGEMENT.CONDITIONS
    COMMISSION.AMOUNT = R.AA.CHARGE<AA.CHG.FIXED.AMOUNT>

    IF R.NEW(AA.PS.LOCAL.REF)<1, TOTAL.INTEREST.POS> AND DISCOUNTINT.AMOUNT NE R.NEW(AA.PS.LOCAL.REF)<1, TOTAL.INTEREST.POS> THEN
        AF = AA.PS.LOCAL.REF
        AV = TOTAL.INTEREST.POS
        ETEXT = "AA-DISCOUNTINT.MISMATCH":FM:R.NEW(AA.PS.LOCAL.REF)<1, TOTAL.INTEREST.POS>:VM:DISCOUNTINT.AMOUNT
        CALL STORE.END.ERROR
    END

    IF R.NEW(AA.PS.LOCAL.REF)<1, TOTAL.COMM.POS> AND COMMISSION.AMOUNT NE R.NEW(AA.PS.LOCAL.REF)<1, TOTAL.COMM.POS> THEN
        AF = AA.PS.LOCAL.REF
        AV = TOTAL.COMM.POS
        ETEXT = "AA-COMMISSION.MISMATCH":FM:R.NEW(AA.PS.LOCAL.REF)<1, TOTAL.COMM.POS>:VM:COMMISSION.AMOUNT
        CALL STORE.END.ERROR
    END

    RETURN

*** </region>

*** <region name= GET.ARRANGEMENT.CONDITIONS>

GET.ARRANGEMENT.CONDITIONS:

    returnConditions = ''
    PROPERTY.CLASS = "CHARGE"
    R.AA.CHARGE = ""
    CALL AA.GET.ARRANGEMENT.CONDITIONS(ARR.ID, PROPERTY.CLASS, property, effectiveDate, '', returnConditions, '')
    R.AA.CHARGE = RAISE(returnConditions)

    RETURN

*** </region>

END
