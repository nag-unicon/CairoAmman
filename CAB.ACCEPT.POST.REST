    SUBROUTINE CAB.ACCEPT.POST.REST

    $INCLUDE T24.BP I_COMMON
    $INCLUDE T24.BP I_EQUATE
    $INCLUDE T24.BP I_GTS.COMMON
    $INCLUDE T24.BP I_F.CUSTOMER
    $INCLUDE T24.BP I_F.ACCOUNT
    $INCLUDE T24.BP I_F.POSTING.RESTRICT
    $INCLUDE T24.BP I_F.FUNDS.TRANSFER
    $INCLUDE T24.BP I_F.AC.LOCKED.EVENTS
    $INCLUDE TAM.BP I_F.MDC.H.DATA.CAPTURE
    $INCLUDE CAB.BP I_F.CAB.INTRF.POST.REST

    IF ID.NEW[1,2] EQ 'FT' OR ID.NEW[1,3] EQ 'BDC' OR ID.NEW[1,4] EQ 'ACLK' THEN
        GOSUB OPEN.FILES
        GOSUB PROCESS
    END

    RETURN

OPEN.FILES:
    FN.INTRF.POST.REST = 'F.CAB.INTRF.POST.REST'
    F.INTRF.POST.REST = ''
    CALL OPF(FN.INTRF.POST.REST,F.INTRF.POST.REST)

    FN.ACCOUNT = 'F.ACCOUNT'
    F.ACCOUNT = ''
    CALL OPF(FN.ACCOUNT,F.ACCOUNT)

    FN.POST.REST = 'F.POSTING.RESTRICT'
    F.POST.REST = ''
    CALL OPF(FN.POST.REST,F.POST.REST)

    FN.CUSTOMER = 'F.CUSTOMER'
    F.CUSTOMER = ''
    CALL OPF(FN.CUSTOMER,F.CUSTOMER)

    FN.MDC = 'F.MDC.H.DATA.CAPTURE'
    F.MDC = ''
    CALL OPF(FN.MDC,F.MDC)

    INTRF.ID = OFS$SOURCE.ID
    CALL F.READ(FN.INTRF.POST.REST,INTRF.ID,R.INTRF.POST.REST,F.INTRF.POST.REST,INTRF.ERR)

    RETURN

PROCESS:

    BEGIN CASE
    CASE ID.NEW[1,2] EQ 'FT'
        DEBIT.ACCT = R.NEW(FT.DEBIT.ACCT.NO)
        CREDIT.ACCT = R.NEW(FT.CREDIT.ACCT.NO)
        PR.ACCT = '' ; POSTING.REST.ID = ''

        TXN.FLAG = 'DEBIT'
        PR.ACCT = DEBIT.ACCT
        GOSUB CHECK.ACCOUNT

        IF TEXT NE 'N' THEN
            PR.ACCT = '' ; POSTING.REST.ID = ''

            TXN.FLAG = 'CREDIT'
            PR.ACCT = CREDIT.ACCT
            GOSUB CHECK.ACCOUNT

        END
    CASE ID.NEW[1,4] EQ 'ACLK'
        PR.ACCT = '' ; POSTING.REST.ID = ''
        TXN.FLAG = 'DEBIT'
        PR.ACCT = R.NEW(AC.LCK.ACCOUNT.NUMBER)
        GOSUB CHECK.ACCOUNT
    CASE ID.NEW[1,3] EQ 'BDC'
        DR.ACCT = R.NEW(MDC.DR.ACCOUNT.NO)
        CR.ACCT = R.NEW(MDC.CR.ACCOUNT.NO)
        PR.ACCT = '' ; POSTING.REST.ID = ''
        LOOP
            CUR.ACCT = ''
            AC.POS = ''
            REMOVE CUR.ACCT FROM DR.ACCT SETTING AC.POS
        WHILE CUR.ACCT:AC.POS
            PR.ACCT = '' ; POSTING.REST.ID = ''

            TXN.FLAG = 'DEBIT'
            PR.ACCT = CUR.ACCT
            IF TEXT NE 'N' THEN
                GOSUB CHECK.ACCOUNT
            END ELSE
                RETURN
            END

        REPEAT
        PR.ACCT = '' ; POSTING.REST.ID = ''
        LOOP
            CUR.ACCT = ''
            AC.POS = ''
            REMOVE CUR.ACCT FROM CR.ACCT SETTING AC.POS
        WHILE CUR.ACCT:AC.POS
            PR.ACCT = '' ; POSTING.REST.ID = ''

            TXN.FLAG = 'CREDIT'
            PR.ACCT = CUR.ACCT
            IF TEXT NE 'N' THEN
                GOSUB CHECK.ACCOUNT
            END ELSE
                RETURN
            END

        REPEAT
    CASE 1
    END CASE

    RETURN

CHECK.ACCOUNT:
    CALL F.READ(FN.ACCOUNT,PR.ACCT,R.ACCT,F.ACCOUNT,AC.ERR)
    POSTING.REST.ID = R.ACCT<AC.POSTING.RESTRICT>
    GOSUB CHECK.POSTING.RESTRICT
    IF TEXT NE 'N' AND R.ACCT<AC.CUSTOMER> NE '' THEN
        CUST.ID = R.ACCT<AC.CUSTOMER>
        CALL F.READ(FN.CUSTOMER,CUST.ID,R.CUST.REC,F.CUSTOMER,CUS.ERR)
        POSTING.REST.ID = R.CUST.REC<EB.CUS.POSTING.RESTRICT>
        GOSUB CHECK.POSTING.RESTRICT
    END

    RETURN

CHECK.POSTING.RESTRICT:
    R.POST.REST = ''
    CALL F.READ(FN.POST.REST,POSTING.REST.ID,R.POST.REST,F.POST.REST,PR.ERR)
    BEGIN CASE
    CASE TXN.FLAG EQ 'DEBIT'
        IF R.POST.REST<AC.POS.RESTRICTION.TYPE> EQ 'DEBIT' OR R.POST.REST<AC.POS.RESTRICTION.TYPE> EQ 'ALL' THEN
            LOCATE POSTING.REST.ID IN R.INTRF.POST.REST<INTRF.PR.ALLOWED.POST.REST,1> SETTING AL.POS THEN
                TEXT = 'Y'
            END ELSE
                LOCATE POSTING.REST.ID IN R.INTRF.POST.REST<INTRF.PR.ERROR.POST.REST,1> SETTING AL.POS THEN
                    E = "Error Posting Restriction-":POSTING.REST.ID
                    CALL ERR
                END
                TEXT = 'N'
            END
        END
    CASE TXN.FLAG EQ 'CREDIT'
        IF R.POST.REST<AC.POS.RESTRICTION.TYPE> EQ 'CREDIT' OR R.POST.REST<AC.POS.RESTRICTION.TYPE> EQ 'ALL' THEN
            LOCATE POSTING.REST.ID IN R.INTRF.POST.REST<INTRF.PR.ALLOWED.POST.REST,1> SETTING AL.POS THEN
                TEXT = 'Y'
            END ELSE
                LOCATE POSTING.REST.ID IN R.INTRF.POST.REST<INTRF.PR.ERROR.POST.REST,1> SETTING AL.POS THEN
                    E = "Error Posting Restriction-":POSTING.REST.ID
                    CALL ERR
                END
                TEXT = 'N'
            END
        END
    END CASE
    RETURN
END
