    SUBROUTINE CAB.AA.LOAN.BALANCE.LIST(RET.ARRAY)

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_ENQUIRY.COMMON
    $INSERT I_F.AA.ARRANGEMENT
    $INSERT I_F.ACCOUNT
    $INSERT I_F.CUSTOMER
    $INSERT I_F.AA.ACCOUNT
    $INSERT I_F.AA.ACCOUNT.DETAILS

    GOSUB INIT
    GOSUB GET.INPUT.DATA

    IF ENQ.SELECTION<1> EQ 'CAB.AA.BALANCE.LIST' THEN
        IF CO.FLG OR BAL.FLG OR CUS.FLG OR ARR.FLG OR PRO.FLG THEN
            SORT.BY.ARR = ' BY PRODUCT BY CUSTOMER BY CO.CODE'
            GOSUB PROCESS
        END
    END ELSE
        SORT.BY.ARR = ' BY PRODUCT BY CUSTOMER BY CO.CODE'
        GOSUB PROCESS
        GOSUB CONVER.TO.CSV
    END
    RETURN


CONVER.TO.CSV:

    OPEN '','&SAVEDLISTS&' TO F$SAVEDLISTS ELSE
        ERR.OPEN ='EB.RTN.CANT.OPEN.&SAVEDLISTS'
    END

    KEY.FORMAT = "AA.BALANCE.DETAILS-":TODAY:".csv"


    FIN.RET.ARRAY <-1>= 'Branch_Name':"*":'Product_Name':"*":'Category':"*":'Officer':"*":'Customer':"*":'Customer_Name':"*":'Arrangement_Id':"*":'Loan_Account_No':"*":'Balance_Type'
    FIN.RET.ARRAY:="*":'Balance_Amt':"*":'Currency':"*":'Customer_Status':"*":'Original_Amount':"*":'Accural_Status':"*":'Overdue_status':"*":'Legal_Status'

    FIN.RET.ARRAY<-1> = RET.ARRAY
    CONVERT '*' TO ','IN FIN.RET.ARRAY
    WRITE FIN.RET.ARRAY TO F$SAVEDLISTS,KEY.FORMAT
    RETURN


INIT:

    FN.AA.ARR = 'F.AA.ARRANGEMENT'
    F.AA.ARR=''
    CALL OPF(FN.AA.ARR,F.AA.ARR)

    FN.CUS= 'F.CUSTOMER'
    F.CUS = ''
    CALL OPF(FN.CUS,F.CUS)

    FN.ACC = 'F.ACCOUNT'
    F.ACC=''
    CALL OPF(FN.ACC,F.ACC)


    APP.NAME = 'AA.ARR.ACCOUNT'
    FIELD.NAME='LEGAL.STATUS'
    FIELD.POS = '';
    CALL MULTI.GET.LOC.REF(APP.NAME,FIELD.NAME,FIELD.POS)
    LEG.POS = FIELD.POS<1,1>

    EFF.DATE = TODAY
    RETURN

GET.INPUT.DATA:
    SEL.LIST = ' WITH PRODUCT.LINE EQ LENDING'

    LOCATE 'COMPANY' IN ENQ.SELECTION<2,1> SETTING CO.POS THEN
        CO.ID = ENQ.SELECTION<4,CO.POS>
        SEL.LIST <-1> = ' AND CO.CODE EQ ': CO.ID
        CO.FLG = '1'
    END

    LOCATE 'BALANCE.TYPE' IN ENQ.SELECTION<2,1> SETTING B.POS THEN
        BAL.TYPE = ENQ.SELECTION<4,B.POS>
        TOT.BAL.LIST = BAL.TYPE
        BAL.FLG = '1'
    END ELSE
        BALANCE.TYPE = 'ELOANBALANCELIST'
        CALL F.READ("F.AC.BALANCE.TYPE",BALANCE.TYPE,REC.ID,'',REC.ERR)
        BAL.LIST = REC.ID<7>
        TOT.BAL.LIST = BAL.LIST
    END

    LOCATE 'CUSTOMER' IN ENQ.SELECTION<2,1> SETTING CUS.POS THEN
        CUS.NO = ENQ.SELECTION<4,CUS.POS>
        SEL.LIST <-1> = ' AND CUSTOMER EQ ': CUS.NO
        CUS.FLG = '1'
    END

    LOCATE 'ARRANGEMENT' IN ENQ.SELECTION<2,1> SETTING ARR.POS THEN
        ARR.ID = ENQ.SELECTION<4,ARR.POS>
        SEL.LIST <-1> = ' AND @ID EQ ':ARR.ID
        ARR.FLG = '1'
    END

    LOCATE 'PRODUCT' IN ENQ.SELECTION<2,1> SETTING PRO.ID THEN
        PRO.ID = ENQ.SELECTION<4,PRO.ID>
        SEL.LIST<-1> = ' AND PRODUCT EQ ': PRO.ID
        PRO.FLG = '1'
    END
    CONVERT FM TO ' ' IN SEL.LIST
    RETURN

PROCESS:
    SEL.CMD = 'SELECT ': FN.AA.ARR : SEL.LIST : SORT.BY.ARR
    SEL.LIST = ''
    CALL EB.READLIST(SEL.CMD,SEL.LIST,'',NO.OF.REC,SEL.ERR)
    LOOP
        REMOVE ARR.ID FROM SEL.LIST SETTING ARR.POS
    WHILE ARR.ID : ARR.POS
        ADD.FLG ='1';ARR.REC = '';PRODUCT.ID = '';PROPERTY.LIST = '';
        CALL AA.GET.ARRANGEMENT.PRODUCT(ARR.ID,EFF.DATE,ARR.REC,PRODUCT.ID,PROPERTY.LIST)
        CUS.NO = ARR.REC<AA.ARR.CUSTOMER>
        CO.CODE = ARR.REC<AA.ARR.CO.CODE>
        PROD.ID = PRODUCT.ID
        ACC.NO = ARR.REC<AA.ARR.LINKED.APPL.ID>
        CURR = ARR.REC<AA.ARR.CURRENCY>
        GOSUB GET.ACC.DETAILS
        GOSUB GET.CUS.DETAILS
        GOSUB GET.LEGAL.DETAILS
        UPDATE.INFO = ''; PROCESS.TYPE = 'INITIALISE'
        CALL AA.PROCESS.ACCOUNT.DETAILS(ARR.ID, PROCESS.TYPE, UPDATE.TYPE,UPDATE.INFO, RET.ERROR)
        ST.DT  = UPDATE.INFO<AA.AD.START.DATE>
        END.DT = TODAY
        ACC.STATUS = UPDATE.INFO<AA.AD.SUSP.STATUS>
        LOAN.OD.STATUS = UPDATE.INFO<AA.AD.ARR.AGE.STATUS>
        GOSUB GET.ACCT.BALANCES
        GOSUB OD.DUES

    REPEAT


    RETURN

GET.LEGAL.DETAILS:
    CALL AA.GET.ARRANGEMENT.CONDITIONS(ARR.ID,'ACCOUNT','',EFF.DATE,R.ACCOUNT.ID,R.ACCOUNT.COND,ACCT.ERR)
    R.ACCOUNT.COND = RAISE(R.ACCOUNT.COND)
    LEG.STATUS= R.ACCOUNT.COND<AA.AC.LOCAL.REF><1,LEG.POS>

    RETURN
GET.ACCT.BALANCES:
    CALL AA.GET.ARRANGEMENT.CONDITIONS(ARR.ID,'TERM.AMOUNT','',EFF.DATE,R.T.AMT.ID,R.T.AMT.COND,T.AMT.ERR)
    R.T.AMT.COND = RAISE(R.T.AMT.COND)

    T.COMMIT = '';
    BALANCE.TYPE="TOT":R.T.AMT.ID
    CALL AA.GET.PERIOD.BALANCES(ACC.NO,BALANCE.TYPE,REQUEST.TYPE,ST.DT,END.DT,'',T.BAL.DETAILS,RET.ERR)
    T.COMMIT = T.BAL.DETAILS<4>
    NO.OF.TOT.COMM = DCOUNT(T.COMMIT,VM)
    T.COMMIT = T.COMMIT<1,NO.OF.TOT.COMM>
    T.COMMIT = ABS(T.COMMIT)
    T.COMMIT = FMT(T.COMMIT,"R3#19")
    RETURN

GET.CUS.DETAILS:

    CALL F.READ(FN.CUS,CUS.NO,R.CUS.DET,F.CUS,ERR.CUS)
    CUS.NAME  = R.CUS.DET<EB.CUS.SHORT.NAME,1>
    CUS.STATUS = R.CUS.DET<EB.CUS.CUSTOMER.STATUS>



    RETURN
GET.ACC.DETAILS:

    CALL F.READ(FN.ACC,ACC.NO,R.ACCOUNT.DETAILS,F.ACC,ERR.ACC)
    CATE = R.ACCOUNT.DETAILS<AC.CATEGORY>
    OFFICER = R.ACCOUNT.DETAILS<AC.ACCOUNT.OFFICER>

    RETURN
OD.DUES:
    BD.BAL = ''; BAL.POS = ''
    BAL.LIST = TOT.BAL.LIST
    LOOP
        REMOVE R.ID FROM BAL.LIST SETTING BAL.POS
    WHILE R.ID : BAL.POS
        BAL.DETAILS = ''; REQUEST.TYPE = '';
        CALL AA.GET.PERIOD.BALANCES(ACC.NO,R.ID,REQUEST.TYPE,ST.DT,END.DT,'',BAL.DETAILS,BAL.ERR)
        NO.OF.DT = DCOUNT(BAL.DETAILS<1>,VM)
        BD.BAL = BAL.DETAILS<4,NO.OF.DT>
        IF BD.BAL THEN
            BD.BAL = ABS(BD.BAL)
            BD.BAL = FMT(BD.BAL,"R3#19")
            GOSUB RETURN.ARRAY
            ADD.FLG = ''
        END
    REPEAT

    RETURN

RETURN.ARRAY:
    IF ADD.FLG THEN
        RET.ARRAY<-1> = CO.CODE : "*":PROD.ID :"*": CATE :"*": OFFICER:"*": CUS.NO : "*": CUS.NAME : "*":ARR.ID:"*":ACC.NO :"*":R.ID : "*" :BD.BAL
        RET.ARRAY:= "*":CURR : "*" : CUS.STATUS : "*" : T.COMMIT :"*": ACC.STATUS : "*": LOAN.OD.STATUS : "*": LEG.STATUS

    END ELSE

        RET.ARRAY <-1> = "*":"*":"*":"*":"*":"*":"*":"*":R.ID:"*":BD.BAL:"*":"*":"*":"*":"*":"*"
    END
    RETURN

END
