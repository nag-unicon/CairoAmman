*-----------------------------------------------------------------------------
* <Rating>140</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE AA.RECALCUALTE.PAYMENT.SCHEDULE(ARR.ID)
* ----------------------------------------------------------------------------------------------
* Company Name     : Cairo Amman Bank
* Developed By     : Muthukaruppan S
* Development Id   : ITSS
* Date             : 07th Sep 2017
*----------------------------------------------------------------------------------------------
* Subroutine Type  : Batch Routine
* Attached to      : BNK/AA.RECALCUALTE.PAYMENT.SCHEDULE
*
* Attached As      : Batch Routine
* ----------------------------------------------------------------------------------------------
* Primary Purpose  : This routine used to recalculate the payments for all the arrangements which is in Balloon Payment.
* ----------------------------------------------------------------------------------------------
* ARGS -(Input)    : NA
* ARGS -(Output)   : NA
* ----------------------------------------------------------------------------------------------
* Modification History:
* ---------------------
* Modification Ref :
* Modification Date:
* Modified by      :
* Modifi. Descript :
* ----------------------------------------------------------------------------------------------

*** <region name= Inserts>
***
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.AA.ARRANGEMENT
    $INSERT I_F.AA.PAYMENT.SCHEDULE
    $INSERT I_F.AA.ARRANGEMENT.ACTIVITY

*** </region>
*** <region name= Main Body>
    ARR.ID = TRIM(ARR.ID)
    PROCESS.FLAG = ""

    GOSUB GET.ARRANGEMENT.COMPANY
    GOSUB GET.SCHEDULE.RECORD
    GOSUB FORM.ARRANGEMENT.ACTIVITY
    GOSUB FORM.SCHEDULE.ACTIVITY
    GOSUB PROCESS.OFS.MESSAGE

    RETURN

*** </region>

GET.ARRANGEMENT.COMPANY:

    CALL AA.GET.ARRANGEMENT(ARR.ID, R.AA.ARRANGEMENT, RET.ERROR)
    ARR.COMPANY = R.AA.ARRANGEMENT<AA.ARR.CO.CODE>

    RETURN

FORM.ARRANGEMENT.ACTIVITY:

    R.AA.ARRANGEMENT.ACTIVITY = ""
    R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.ARRANGEMENT>    = ARR.ID
    R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.ACTIVITY>       = "LENDING-CHANGE-REPAYMENT.SCHEDULE"
    R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.EFFECTIVE.DATE> = TODAY
    R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.PROPERTY,1>     = "REPAYMENT.SCHEDULE"
    FIELD.CNT = 0

    RETURN

GET.SCHEDULE.RECORD:

    idPropertyClass = "PAYMENT.SCHEDULE"
    idProperty = ""
    effectiveDate = ""
    returnIds = ""
    returnConditions = ""

    CALL AA.GET.ARRANGEMENT.CONDITIONS(ARR.ID, idPropertyClass, idProperty, effectiveDate, returnIds, returnConditions, returnError)
    R.AA.PAYMENT.SCHEDULE = RAISE(returnConditions)

    RETURN

FORM.SCHEDULE.ACTIVITY:

    TOTAL.PAYMENT = DCOUNT(R.AA.PAYMENT.SCHEDULE<AA.PS.PAYMENT.TYPE>, VM)

    ACTIVITY.COUNT = 0

    PAY.TYPE.CNT = 1
    RECORD.ARRAY = ""
    LOOP
    WHILE PAY.TYPE.CNT LE TOTAL.PAYMENT

        ACTIVITY.COUNT + = 1
        FIELD.CNT + =1

        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.NAME,1,FIELD.CNT> = 'PAYMENT.TYPE:':ACTIVITY.COUNT:':1'
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.VALUE,1,FIELD.CNT> = R.AA.PAYMENT.SCHEDULE<AA.PS.PAYMENT.TYPE, PAY.TYPE.CNT>

        FIELD.CNT + = 1
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.NAME,1,FIELD.CNT> = 'PAYMENT.METHOD:':ACTIVITY.COUNT:':1'
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.VALUE,1,FIELD.CNT> = R.AA.PAYMENT.SCHEDULE<AA.PS.PAYMENT.METHOD, PAY.TYPE.CNT>

        FIELD.CNT+=1
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.NAME,1,FIELD.CNT> = 'PAYMENT.FREQ:':ACTIVITY.COUNT:':1'
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.VALUE,1,FIELD.CNT> = R.AA.PAYMENT.SCHEDULE<AA.PS.PAYMENT.FREQ, PAY.TYPE.CNT>

        FIELD.CNT+=1
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.NAME,1,FIELD.CNT> = 'PROPERTY:':ACTIVITY.COUNT:':1'
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.VALUE,1,FIELD.CNT> = R.AA.PAYMENT.SCHEDULE<AA.PS.PROPERTY, PAY.TYPE.CNT,1>

        IF R.AA.PAYMENT.SCHEDULE<AA.PS.PROPERTY, PAY.TYPE.CNT, 2> THEN
            FIELD.CNT+=1
            R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.NAME,1,FIELD.CNT> = 'PROPERTY:':ACTIVITY.COUNT:':2'
            R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.VALUE,1,FIELD.CNT> = R.AA.PAYMENT.SCHEDULE<AA.PS.PROPERTY, PAY.TYPE.CNT,2>
        END

        FIELD.CNT+=1
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.NAME,1,FIELD.CNT> = 'START.DATE:':ACTIVITY.COUNT:':1'
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.VALUE,1,FIELD.CNT> = R.AA.PAYMENT.SCHEDULE<AA.PS.START.DATE, PAY.TYPE.CNT>

        IF R.AA.PAYMENT.SCHEDULE<AA.PS.END.DATE, PAY.TYPE.CNT> THEN
            FIELD.CNT+=1
            R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.NAME,1,FIELD.CNT> = 'END.DATE:':ACTIVITY.COUNT:':1'
            R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.VALUE,1,FIELD.CNT> = R.AA.PAYMENT.SCHEDULE<AA.PS.END.DATE, PAY.TYPE.CNT>
        END

        FIELD.CNT+=1
        IF ACTIVITY.COUNT EQ 1 THEN
            R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.NAME,1,FIELD.CNT> = 'ACTUAL.AMT:':ACTIVITY.COUNT:':1'
            R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.VALUE,1,FIELD.CNT> = ""
        END

        IF ACTIVITY.COUNT EQ 2 THEN
            FIELD.CNT+=1
            R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.NAME,1,FIELD.CNT> = 'ACTUAL.AMT:':ACTIVITY.COUNT:':1'
            R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.VALUE,1,FIELD.CNT> = R.AA.PAYMENT.SCHEDULE<AA.PS.ACTUAL.AMT, PAY.TYPE.CNT, 1>
        END

        PAY.TYPE.CNT + = 1

    REPEAT

    FIELD.CNT+=1
    R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.NAME,1,FIELD.CNT> = 'INCLUDE.PRIN.PAYMENTS:':'1':':1'
    R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.VALUE,1,FIELD.CNT> = 'YES'


    TOTAL.ON.ACTIVITY = DCOUNT(R.AA.PAYMENT.SCHEDULE<AA.PS.ON.ACTIVITY>, VM)

    ACT.CNT = 1

    LOOP
    WHILE ACT.CNT LE TOTAL.ON.ACTIVITY

        ACTIVITY.COUNT + = 1
        FIELD.CNT + =1

        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.NAME,1,FIELD.CNT>  = 'ON.ACTIVITY:':ACT.CNT:':1'
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.VALUE,1,FIELD.CNT> = R.AA.PAYMENT.SCHEDULE<AA.PS.ON.ACTIVITY, ACT.CNT>

        ACTIVITY.COUNT + = 1
        FIELD.CNT + =1
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.NAME,1,FIELD.CNT>  = 'RECALCULATE:':ACT.CNT:':1'
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.VALUE,1,FIELD.CNT> = R.AA.PAYMENT.SCHEDULE<AA.PS.RECALCULATE, ACT.CNT>


        ACT.CNT + = 1
    REPEAT

    IF R.AA.PAYMENT.SCHEDULE<AA.PS.APPLY.PAYMENT> THEN

        FIELD.CNT + =1
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.NAME,1,FIELD.CNT> = 'APPLY.PAYMENT:':'1':':1'
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.VALUE,1,FIELD.CNT> = R.AA.PAYMENT.SCHEDULE<AA.PS.APPLY.PAYMENT>

    END

    IF R.AA.PAYMENT.SCHEDULE<AA.PS.RECALC.FREQUENCY> THEN
        FIELD.CNT + =1
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.NAME,1,FIELD.CNT>  = 'RECALC.FREQUENCY:':'1':':1'
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.VALUE,1,FIELD.CNT> = R.AA.PAYMENT.SCHEDULE<AA.PS.RECALC.FREQUENCY>
    END

    LOCATE "LENDING-CHANGE-REPAYMENT.SCHEDULE" IN R.AA.PAYMENT.SCHEDULE<AA.PS.ON.ACTIVITY,1> SETTING ON.ACT.POS ELSE
        FIELD.CNT + =1
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.NAME,1,FIELD.CNT>  = 'ON.ACTIVITY:':ACT.CNT:':1'
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.VALUE,1,FIELD.CNT> =  "LENDING-CHANGE-REPAYMENT.SCHEDULE"

        FIELD.CNT + =1
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.NAME,1,FIELD.CNT>  = 'RECALCULATE:':ACT.CNT:':1'
        R.AA.ARRANGEMENT.ACTIVITY<AA.ARR.ACT.FIELD.VALUE,1,FIELD.CNT> = "PAYMENT"
    END

    RETURN

PROCESS.OFS.MESSAGE:

    APP.NAME = "AA.ARRANGEMENT.ACTIVITY"
    OFS.RECORD = ''
    OFSVERSION = APP.NAME:","
    NO.OF.AUTH = "0"
    GTS.MODE = 1
    TRANSACTION.ID = ""
    FUNCT = "I"

    SAVE.ID.COMPANY = ID.COMPANY
    ID.COMPANY = ARR.COMPANY

    CALL OFS.BUILD.RECORD(APP.NAME, FUNCT, "PROCESS", OFSVERSION, GTS.MODE, NO.OF.AUTH, TRANSACTION.ID, R.AA.ARRANGEMENT.ACTIVITY, OFS.RECORD)

    theResponse    = ""
    txnCommitted   = ""
    options        = ""
    options<1>     = "AA.CHARGE"
    options<4>     = "HLD"

    CALL OFS.CALL.BULK.MANAGER(options,OFS.RECORD,theResponse,txnCommitted)

    MESSAGE.TYPE = ""
    ERROR.SOURCE = ""
    ERROR.MESSAGE = ""
    ERR.ID.DETAILS = ""
    CALL AA.GET.ERROR.DETAILS(theResponse, MESSAGE.TYPE, ERROR.SOURCE, ERROR.MESSAGE, ERR.ID.DETAILS)

    ID.COMPANY = SAVE.ID.COMPANY

    RETURN

END
