    SUBROUTINE CAB.B.IBAN.CHANGE.RTN
    $INCLUDE T24.BP I_COMMON
    $INCLUDE T24.BP I_EQUATE
    $INCLUDE T24.BP I_F.ACCOUNT
    $INCLUDE T24.BP I_F.COMPANY
    $INCLUDE T24.BP I_F.EB.COMPANY.CHANGE
    $INCLUDE T24.BP I_GTS.COMMON
    $INSERT T24.BP I_F.OFS.SOURCE
    $INSERT CAB.BP I_F.CAB.IBAN.CATEG


    GOSUB INIT
    GOSUB PROCESS

    RETURN
*****
INIT:
*****
    BBAN = '' ; N.BBAN = '' ; CNTRY.CODE = '' ; SWFIT.CODE = ''
    BRANCH.CODE = ''; ACCT.ID = '' ; I = '' ; M.BBAN = '' ; Y.POS = ''
    IBAN.ACCT = ''
    L.BBAN = ''
    R.BBAN = ''
    F.COMPANY = ''
    FN.COMPANY = 'F.COMPANY'
    CALL OPF(FN.COMPANY,F.COMPANY)

    FN.EB.COMPANY.CHANGE = 'F.EB.COMPANY.CHANGE'
    F.EB.COMPANY.CHANGE = ''
    CALL OPF(FN.EB.COMPANY.CHANGE,F.EB.COMPANY.CHANGE)

    FN.ACCOUNT = 'F.ACCOUNT'
    F.ACCOUNT = ''
    CALL OPF(FN.ACCOUNT,F.ACCOUNT)

    FN.CAB.IBAN.CATEG = 'F.CAB.IBAN.CATEG'
    F.CAB.IBAN.CATEG = ''
    CALL OPF(FN.CAB.IBAN.CATEG,F.CAB.IBAN.CATEG)
    OFS.SOURCE.ID = 'IBAN.OFS'
    RETURN
*********
PROCESS:
*********

    SEL.CMD = 'SELECT ' : FN.EB.COMPANY.CHANGE : ' WITH APPLICATION EQ ACCOUNT AND WITH RESULT EQ ""'
    CALL EB.READLIST(SEL.CMD,SEL.LIST, '',REC.NO,SEL.ERR)
    LOOP
        REMOVE COMCHG.ID FROM SEL.LIST SETTING POS
    WHILE COMCHG.ID:POS
        CALL F.READ(FN.EB.COMPANY.CHANGE,COMCHG.ID,R.COMPANY.CHANGE,F.EB.COMPANY.CHANGE,COMPCHG.ERR)
        ACCT.ID = R.COMPANY.CHANGE<EB.CC.CONTRACT.KEY>
        CALL F.READ(FN.ACCOUNT,ACCT.ID,R.ACCOUNT,F.ACCOUNT,ACCT.ERROR)
        CALL F.READ(FN.CAB.IBAN.CATEG,R.ACCOUNT<AC.CATEGORY>,IBAN.CAT.REC,F.CAB.IBAN.CATEG,CAT.ERROR)

        IF IBAN.CAT.REC<CAB.IBNCAT.INCLUDE.FLAG> EQ 'Y' THEN
            AC.COMP.CODE = R.COMPANY.CHANGE<EB.CC.COMPANY.TO>
            CALL F.READ(FN.COMPANY,AC.COMP.CODE,COMP.REC,F.COMPANY,COMP.ERR)
            IF COMP.REC THEN
                CNTRY.CODE  = COMP.REC<EB.COM.LOCAL.COUNTRY>
                SWFIT.CODE  = COMP.REC<EB.COM.SWIFT.ADD.COM.REF>[1,4]
                BRANCH.CODE = R.COMPANY.CHANGE<EB.CC.COMPANY.TO>[6,4]

                ACCT.ID = FMT(ACCT.ID,'R%18')
                BBAN = CNTRY.CODE:SWFIT.CODE:BRANCH.CODE:ACCT.ID
                N.BBAN = '12101011':BRANCH.CODE:ACCT.ID:'192400'
                Y.BBAN = N.BBAN
                Y.POS = 10
                FOR I = 1 TO 5

                    IF LEN(Y.BBAN) GE 9 THEN
                        N.BBAN = Y.BBAN[Y.POS,LEN(Y.BBAN)]
                        Y.BBAN = Y.BBAN[1,9]
                    END
                    M.BBAN = MOD (Y.BBAN,97)
                    M.BBAN = FMT(M.BBAN,'R%2')
                    IF I LT 5 THEN
                        Y.BBAN = M.BBAN:N.BBAN
                    END
                NEXT
                M.BBAN = FMT(98 - M.BBAN,'R%2')
                IBAN.ACCT = CNTRY.CODE:M.BBAN:SWFIT.CODE:BRANCH.CODE:ACCT.ID
                Y.MESSAGE = 'ACCOUNT,INP/I/PROCESS,//' : R.COMPANY.CHANGE<EB.CC.COMPANY.FROM> :',': R.COMPANY.CHANGE<EB.CC.CONTRACT.KEY>:',ALT.ACCT.ID:3:1=':IBAN.ACCT
                CALL OFS.POST.MESSAGE(Y.MESSAGE,MESSAGE.ID,OFS.SOURCE.ID,'')
            END
        END
    REPEAT
    RETURN
END
